Jailbreak = Jailbreak
hook_Add = hook.Add

-- Disallow player shoot on marker setup
do

    IN_ATTACK = IN_ATTACK
    IN_WALK = IN_WALK

    hook_Add "StartCommand", "Jailbreak::Markers", ( cmd ) =>
        if @Alive! and cmd\KeyDown( IN_WALK )
            cmd\RemoveKey( IN_ATTACK )
            cmd\RemoveKey( IN_ATTACK2 )

-- Funny run and crouch animations
do

    singleHandHoldTypes = {
        grenade: true
        normal: true
        melee: true
        knife: true
        fist: true
        slam: true
    }

    MOVETYPE_NOCLIP = MOVETYPE_NOCLIP

    hook_Add "CalcMainActivity", "Jailbreak::Additional Animations", ( velocity ) =>
        if not @IsOnGround! or @GetMoveType! == MOVETYPE_NOCLIP
            return

        if velocity\Length! >= @GetRunSpeed! * 0.95
            weapon = @GetActiveWeapon!
            if weapon and weapon\IsValid!
                holdType = weapon\GetHoldType!
                if holdType == "normal" and @IsPrisoner!
                    return ACT_HL2MP_RUN_PANICKED, -1

                if singleHandHoldTypes[ weapon\GetHoldType! ]
                    return ACT_HL2MP_RUN_FAST, -1

        elseif @Crouching! and velocity\Length2DSqr! < 1
            weapon = @GetActiveWeapon!
            if weapon and weapon\IsValid! and weapon\GetHoldType! ~= "normal"
                return

            return ACT_MP_JUMP, @LookupSequence( "pose_ducking_01" )

-- Silly death animations
hook_Add "FinishMove", "Jailbreak::Death Animations", ( mv ) =>
    if @GetNW2Int( "death-animation" ) ~= 0
        return true

hook_Add "PlayerEmitSound", "Jailbreak::Death Animations", ( data ) =>
    if @GetNW2Int( "death-animation" ) ~= 0
        return false

-- TF2 Double Jump
hook_Add "Move", "Jailbreak::TF2 Double Jump", ( mv ) =>
    if Jailbreak.GameName ~= "tf" or @WaterLevel! > 1
        return

    if @IsOnGround!
        @HasDoubleJump = true
    elseif @HasDoubleJump and mv\KeyPressed( IN_JUMP )
        @HasDoubleJump = false

        velocity = mv\GetVelocity!
        velocity[ 3 ] = @GetJumpPower! * 2
        mv\SetVelocity( velocity )

-- Player can't take damage in death animation
hook_Add "CanPlayerTakeDamage", "Jailbreak::Death Animations", =>
    if @GetNW2Int( "death-animation" ) == 2
        return false

-- Developer features
do

    IN_JUMP = IN_JUMP
    IN_DUCK = IN_DUCK
    IN_SPEED = IN_SPEED
    IN_FORWARD = IN_FORWARD
    IN_BACK = IN_BACK
    IN_MOVELEFT = IN_MOVELEFT
    IN_MOVERIGHT = IN_MOVERIGHT

    sv_gravity = GetConVar( "sv_gravity" )
    FrameTime = FrameTime
    Lerp = Lerp
    :band = bit
    :abs = math

    hook_Add "Move", "Jailbreak::Developer", ( mv ) =>
        unless @IsFlightAllowed!
            if @GetNW2Bool( "in-flight" )
                @SetNW2Bool( "in-flight", false )

            return

        if @IsOnGround!
            if @GetNW2Bool( "in-flight" )
                @SetNW2Bool( "in-flight", false )

        elseif @GetNW2Bool( "in-flight" )
            velocity = mv\GetVelocity!
            angles = mv\GetMoveAngles!

            buttons = mv\GetButtons!

            if band( buttons, IN_FORWARD ) ~= 0
                velocity += angles\Forward! * 16
                buttons -= IN_FORWARD

            if band( buttons, IN_BACK ) ~= 0
                velocity += angles\Forward! * -16
                buttons -= IN_BACK

            if band( buttons, IN_MOVELEFT ) ~= 0
                velocity += angles\Right! * -16
                buttons -= IN_MOVELEFT

            if band( buttons, IN_MOVERIGHT ) ~= 0
                velocity += angles\Right! * 16
                buttons -= IN_MOVERIGHT

            if band( buttons, IN_JUMP ) ~= 0
                velocity += angles\Up! * 16
                buttons -= IN_JUMP

            if band( buttons, IN_DUCK ) ~= 0
                velocity += angles\Up! * -16
                buttons -= IN_DUCK

            frameTime = FrameTime!

            if band( buttons, IN_SPEED ) ~= 0
                velocity[ 1 ] = Lerp( frameTime, velocity[ 1 ], 0 )
                velocity[ 2 ] = Lerp( frameTime, velocity[ 2 ], 0 )
                velocity[ 3 ] = Lerp( frameTime, velocity[ 3 ], 0 )
                buttons -= IN_SPEED

            if abs( velocity[ 1 ] ) < 1
                velocity[ 1 ] = 0

            if abs( velocity[ 2 ] ) < 1
                velocity[ 2 ] = 0

            if abs( velocity[ 3 ] ) < 1
                velocity[ 3 ] = 0

            velocity[ 3 ] += ( @GetGravity! + 1 ) * sv_gravity\GetFloat! * 0.5 * frameTime

            mv\SetVelocity( velocity )
            mv\SetButtons( buttons )

        elseif mv\KeyPressed( IN_JUMP )
            @SetNW2Bool( "in-flight", true )

hook_Add "GetFallDamage", "Jailbreak::Developer", =>
    if @GetNW2Bool( "in-flight" )
        return 0

hook_Add "UpdateAnimation", "Jailbreak::Developer", ( velocity ) =>
    if @GetNW2Bool( "in-flight" )
        @SetPlaybackRate( velocity\Length! < 128 and 0.25 or 0 )
        return true

do

    ACT_HL2MP_IDLE = ACT_HL2MP_IDLE
    ACT_INVALID = ACT_INVALID
    ACT_MP_SWIM = ACT_MP_SWIM
    IsValid = IsValid

    hook_Add "CalcMainActivity", "Jailbreak::Developer", =>
        unless @GetNW2Bool( "in-flight" )
            return

        act = ACT_INVALID
        if IsValid( @GetActiveWeapon! )
            act = ACT_MP_SWIM
        else
            act = ACT_HL2MP_IDLE + 9

        return act, ACT_INVALID


do

    CurTime = CurTime

    hook_Add "Move", "Jailbreak::weapon_stunstick", ( mv ) =>
        shockTime = @GetNW2Int( "shock-time" )
        if shockTime == 0 or CurTime! > shockTime
            return

        mv\SetMaxSpeed( @GetWalkSpeed! / 4 )
