Jailbreak = Jailbreak
hook_Add = hook.Add

-- Disallow player shoot on marker setup
do

    IN_ATTACK = IN_ATTACK
    IN_WALK = IN_WALK

    hook_Add "StartCommand", "Jailbreak::Markers", ( cmd ) =>
        if @Alive! and cmd\KeyDown( IN_WALK ) and cmd\KeyDown( IN_ATTACK )
            cmd\RemoveKey( IN_ATTACK )

-- Funny run and crouch animations
do

    singleHandHoldTypes = {
        grenade: true
        normal: true
        melee: true
        knife: true
        fist: true
        slam: true
    }

    MOVETYPE_NOCLIP = MOVETYPE_NOCLIP

    hook_Add "CalcMainActivity", "Jailbreak::Additional Animations", ( velocity ) =>
        if not @IsOnGround! or @GetMoveType! == MOVETYPE_NOCLIP
            return

        if velocity\Length! >= @GetRunSpeed! * 0.95
            weapon = @GetActiveWeapon!
            if weapon and weapon\IsValid!
                holdType = weapon\GetHoldType!
                if holdType == "normal" and @IsPrisoner!
                    return ACT_HL2MP_RUN_PANICKED, -1

                if singleHandHoldTypes[ weapon\GetHoldType! ]
                    return ACT_HL2MP_RUN_FAST, -1

        elseif @Crouching! and velocity\Length2DSqr! < 1
            weapon = @GetActiveWeapon!
            if weapon and weapon\IsValid! and weapon\GetHoldType! ~= "normal"
                return

            return ACT_MP_JUMP, @LookupSequence( "pose_ducking_01" )

-- Silly death animations
hook_Add "FinishMove", "Jailbreak::Death Animations", ( mv ) =>
    if @GetNW2Int( "death-animation" ) > 0
        return true

hook_Add "PlayerEmitSound", "Jailbreak::Death Animations", ( data ) =>
    if @GetNW2Int( "death-animation" ) > 0
        return false

-- TF2 Double Jump
hook_Add "Move", "Jailbreak::TF2 Double Jump", ( mv ) =>
    if Jailbreak.GameName ~= "tf" or @WaterLevel! > 1
        return

    if @IsOnGround!
        @HasDoubleJump = true
    elseif @HasDoubleJump and mv\KeyPressed( IN_JUMP )
        @HasDoubleJump = false

        velocity = mv\GetVelocity!
        velocity[ 3 ] = @GetJumpPower! * 2
        mv\SetVelocity( velocity )

-- Player can't take damage in death animation
hook_Add "CanPlayerTakeDamage", "Jailbreak::Death Animations", =>
    if @GetNW2Int( "death-animation" ) == 2
        return false

-- Developer features
do

    IN_JUMP = IN_JUMP
    IN_DUCK = IN_DUCK
    IN_SPEED = IN_SPEED
    IN_FORWARD = IN_FORWARD
    IN_BACK = IN_BACK
    IN_MOVELEFT = IN_MOVELEFT
    IN_MOVERIGHT = IN_MOVERIGHT

    :GetGravity = physenv
    FrameTime = FrameTime
    Lerp = Lerp
    :abs = math

    hook_Add "Move", "Jailbreak::Developer", ( mv ) =>
        unless @IsFlightAllowed!
            return

        if @IsOnGround!
            if @Flying
                @Flying = false

        elseif @Flying
            velocity = mv\GetVelocity!
            angles = mv\GetMoveAngles!

            if mv\KeyDown( IN_FORWARD )
                velocity += angles\Forward! * 16

            if mv\KeyDown( IN_BACK )
                velocity += angles\Forward! * -16

            if mv\KeyDown( IN_MOVELEFT )
                velocity += angles\Right! * -16

            if mv\KeyDown( IN_MOVERIGHT )
                velocity += angles\Right! * 16

            if mv\KeyDown( IN_JUMP )
                velocity += angles\Up! * 16

            if mv\KeyDown( IN_DUCK )
                velocity += angles\Up! * -16

            frameTime = FrameTime!

            if mv\KeyDown( IN_SPEED )
                velocity[ 1 ] = Lerp( frameTime, velocity[ 1 ], 0 )
                velocity[ 2 ] = Lerp( frameTime, velocity[ 2 ], 0 )
                velocity[ 3 ] = Lerp( frameTime, velocity[ 3 ], 0 )

            if abs( velocity[ 1 ] ) < 1
                velocity[ 1 ] = 0

            if abs( velocity[ 2 ] ) < 1
                velocity[ 2 ] = 0

            if abs( velocity[ 3 ] ) < 1
                velocity[ 3 ] = 0

            mv\SetVelocity( velocity - ( @GetGravity! + 1 ) * GetGravity! * frameTime * 0.5 )

        elseif mv\KeyPressed( IN_JUMP )
            @Flying = true

hook_Add "GetFallDamage", "Jailbreak::Developer", =>
    if @Flying
        return 0

hook_Add "UpdateAnimation", "Jailbreak::Developer", ( velocity ) =>
    if @Flying
        @SetPlaybackRate( velocity\Length! < 128 and 0.25 or 0 )
        return true

do

    ACT_HL2MP_IDLE = ACT_HL2MP_IDLE
    ACT_INVALID = ACT_INVALID
    ACT_MP_SWIM = ACT_MP_SWIM
    IsValid = IsValid

    hook_Add "CalcMainActivity", "Jailbreak::Developer", =>
        unless @Flying
            return

        act = ACT_INVALID
        if IsValid( @GetActiveWeapon! )
            act = ACT_MP_SWIM
        else
            act = ACT_HL2MP_IDLE + 9

        return act, ACT_INVALID
