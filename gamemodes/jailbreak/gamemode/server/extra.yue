Jailbreak = Jailbreak

:Clamp, :Rand, :random, :min, :max, :floor = math
ENTITY = FindMetaTable( "Entity" )
:FixModelPath = Jailbreak
:IsValidModel = util
CurTime = CurTime
:Simple = timer
:Add = hook
GM = GM

do

    :white = Jailbreak.Colors

    GM.OnEntityWaterLevelChanged = ( entity, old, new ) =>
        if new >= 2
            if entity\IsOnFire!
                entity\Extinguish!

            if old < new and new == 3 and entity\IsPlayer! and entity\Alive!
                entity.DrownDamageTime = CurTime! + random( 15, 45 )
                entity\SetColor( white )

do

    vector_origin = vector_origin
    DamageInfo = DamageInfo
    LerpVector = LerpVector
    DMG_DROWN = DMG_DROWN

    GM.PlayerPostThink = ( ply ) =>
        unless ply\Alive!
            return

        if ply\IsUnderwater! and ( ply.DrownDamageTime or 0 ) < CurTime!
            ply.DrownDamageTime = CurTime! + 0.5

            damageInfo = DamageInfo!
            damageInfo\SetDamageType( DMG_DROWN )
            damageInfo\SetDamageForce( vector_origin )
            damageInfo\SetAttacker( ply )
            damageInfo\SetDamage( max( 5, ply\Health! * 0.1 ) )
            damageInfo\SetDamagePosition( ply\GetPos! )
            ply\EmitSound( "Player.DrownContinue" )
            ply\TakeDamageInfo( damageInfo )

        if ply\IsBot!
            return

        state = ply\HasSecurityRadio! and ply\GetInfo( "jb_security_radio" ) == "1"
        if state ~= ply\GetNW2Bool( "using-security-radio" )
            ply\SetNW2Bool( "using-security-radio", state )

        if ( ply.NextEyeTargetTime or 0 ) < CurTime!
            ply.NextEyeTargetTime = CurTime! + 0.1

            traceResult = ply\GetEyeTrace!
            if traceResult.Hit
                nextEyePos = LerpVector( 0.5, ply.LastEyePos or vector_origin, traceResult.HitPos )
                ply\SetEyeTarget( nextEyePos )
                ply.LastEyePos = nextEyePos

Simple 0, ->
    :Call = hook
    for addon in *engine.GetAddons!
        if addon.downloaded and addon.mounted
            Call( "WorkshopItemFound", nil, addon.wsid )

if EntityReplacer ~= nil
    :lower, :find = string
    isstring = isstring
    istable = istable

    global ReplaceFilterByModel = ( modelName ) ->
        if isstring( modelName )
            modelName = lower( modelName )
        elseif istable( modelName )
            tbl = {}
            for str in *modelName
                tbl[ lower( str ) ] = true

            modelName = tbl
        else
            return =>
                return true

        return =>
            modelPath = @GetModel!
            unless modelPath
                return false

            modelPath = FixModelPath( modelPath )
            unless IsValidModel( modelPath )
                return false

            if istable( modelName )
                return modelName[ modelPath ]

            return find( modelPath, modelName, 1, false ) ~= nil

    -- https://steamcommunity.com/sharedfiles/filedetails/?id=293904092
    Add "WorkshopItemFound", "Jailbreak::sent_soccerball - Replace", ( wsid ) ->
        if wsid ~= "293904092"
            return

        EntityReplacer( "^prop_physics.*", "sent_soccerball", ReplaceFilterByModel( "models/props_phx/misc/soccerball%.mdl" ) )
        return

-- https://steamcommunity.com/sharedfiles/filedetails/?id=293904092
Add "WorkshopItemFound", "Jailbreak::sent_soccerball - Shop", ( wsid ) ->
    if wsid ~= "293904092"
        return

    Add "ShopItems", "Jailbreak::sent_soccerball", ( add ) ->
        add "sent_soccerball", "models/props_phx/misc/soccerball.mdl", 10, =>
            return @SpawnEntity( "sent_soccerball" )\IsValid!

        return

-- https://steamcommunity.com/sharedfiles/filedetails/?id=931448005
Add "WorkshopItemFound", "Jailbreak::sent_grapplehook_bpack - Shop", ( wsid ) ->
    if wsid ~= "931448005"
        return

    Add "ShopItems", "Jailbreak::sent_grapplehook_bpack", ( add ) ->
        add "sent_grapplehook_bpack", "models/props_phx/wheels/magnetic_small.mdl", 25, =>
            entity = @SpawnEntity( "sent_grapplehook_bpack", => @SetSlotName( "movement" ) )
            if entity\IsValid!
                entity\SetKey( KEY_B )
                return true

            return false

        return

-- https://steamcommunity.com/sharedfiles/filedetails/?id=931376012
Add "WorkshopItemFound", "Jailbreak::sent_jetpack - Shop", ( wsid ) ->
    if wsid ~= "931376012"
        return

    Add "ShopItems", "Jailbreak::sent_jetpack", ( add ) ->
        add "sent_jetpack", "models/thrusters/jetpack.mdl", 30, =>
            return @SpawnEntity( "sent_jetpack", => @SetSlotName( "movement" ) )\IsValid!

        return

-- https://steamcommunity.com/sharedfiles/filedetails/?id=546392647
Add "WorkshopItemFound", "Jailbreak::mediaplayer_tv - Shop", ( wsid ) ->
    if wsid ~= "546392647"
        return

    Add "ShopItems", "Jailbreak::mediaplayer_tv", ( add ) ->
        add "mediaplayer_tv", "models/gmod_tower/suitetv_large.mdl", 45, =>
            entity = @SpawnEntity( "mediaplayer_tv" )
            if entity\IsValid!
                phys = entity\GetPhysicsObject!
                if phys and phys\IsValid!
                    phys\EnableMotion( true )
                    phys\SetMass( 35 )
                    phys\Wake!

                return true

            return false

        return

do

    foodModels = Jailbreak.FoodModels
    unless foodModels
        foodModels = {
            -- GMOD
            "models/food/burger.mdl"
            "models/food/hotdog.mdl"

            -- HL2
            "models/props_c17/doll01.mdl"
            "models/props_junk/garbage_glassbottle001a.mdl"
            "models/props_junk/garbage_glassbottle002a.mdl"
            "models/props_junk/garbage_glassbottle003a.mdl"
            "models/props_junk/garbage_milkcarton002a.mdl"
            "models/props_junk/garbage_milkcarton001a.mdl"
            "models/props_junk/garbage_plasticbottle003a.mdl"
            "models/props_junk/garbage_takeoutcarton001a.mdl"
            "models/props_junk/GlassBottle01a.mdl"
            "models/props_junk/glassjug01.mdl"
            "models/props_junk/watermelon01.mdl"
            "models/props_junk/Shoe001a.mdl"

            -- CSS
            "models/props/CS_militia/bottle01.mdl"
            "models/props/CS_militia/bottle02.mdl"
            "models/props/CS_militia/bottle03.mdl"
            "models/props/cs_office/Snowman_nose.mdl"
            "models/props/cs_office/trash_can_p8.mdl"
            "models/props/cs_office/Water_bottle.mdl"
            "models/props/cs_italy/bananna.mdl"
            "models/props/cs_italy/bananna_bunch.mdl"
            "models/props/cs_italy/banannagib1.mdl"
            "models/props/cs_italy/banannagib2.mdl"
            "models/props/cs_italy/orange.mdl"
            "models/props/cs_italy/orangegib1.mdl"
            "models/props/cs_italy/orangegib2.mdl"
            "models/props/cs_italy/orangegib3.mdl"
            "models/props/de_inferno/crate_fruit_break_gib1.mdl"
            "models/props/de_inferno/crate_fruit_break_gib2.mdl"
            "models/props/de_inferno/crate_fruit_break_gib3.mdl"
            "models/props/de_inferno/goldfish.mdl"

        }

        Jailbreak.FoodModels = foodModels

    Add "PostGamemodeLoaded", "Jailbreak::LoadFoodModels", ->
        for index = 1, #foodModels
            foodModels[ index ] = FixModelPath( foodModels[ index ] )

    cache = {}

    Jailbreak.IsFoodModel = ( modelName ) ->
        unless modelName
            return false

        cached = cache[ modelName ]
        if cached ~= nil
            return cached

        fixedModelPath = FixModelPath( modelName )
        unless IsValidModel( fixedModelPath )
            cache[ modelName ] = false
            return false

        for modelPath in *foodModels
            if fixedModelPath == modelPath
                cache[ modelName ] = true
                return true

do

    foodEatingSound = Sound( "player/eating.wav" )
    :IsFoodModel, :FoodEatingTime = Jailbreak
    CHAN_STATIC = CHAN_STATIC

    Add "OnEntityCreated", "Jailbreak::FoodEntities", =>
        unless @IsProp!
            return

        Simple 0, ->
            if @IsValid! and IsFoodModel( @GetModel! )
                @SetNW2Bool( "is-food", true )

    cache = {}

    Add "PlayerHoldUse", "Jailbreak::FoodEating", ( entity, useTime ) =>
        if useTime < FoodEatingTime\GetFloat! or not entity\IsProp!
            return

        modelPath = entity\GetModel!
        unless IsFoodModel( modelPath )
            return

        healing = cache[ modelPath ]
        unless healing
            mins, maxs = entity\GetCollisionBounds!
            healing = Clamp( mins\Distance( maxs ) / 64, 0, 1 )
            cache[ modelPath ] = healing

        maxHealth = @GetMaxHealth!
        @SetHealth( Clamp( @Health! + floor( maxHealth * healing ), 0, maxHealth ) )

        @EmitSound( foodEatingSound, 50, random( 80, 120 ), Rand( 0.6, 1 ), CHAN_STATIC, 0, 1 )
        entity\Remove!
        return true

do

    lootSound = Sound( "npc/footsteps/softshoe_generic6.wav" )
    :RagdollLootingTime = Jailbreak

    Add "PlayerHoldUse", "Jailbreak::RagdollLooting", ( entity, useTime ) =>
        if useTime < RagdollLootingTime\GetFloat!
            return

        unless entity\IsPlayerRagdoll! or entity\IsRagdoll!
            return

        entity\EmitSound( lootSound, 60, random( 80, 120 ), Rand( 0.7, 1 ), CHAN_STATIC, 0, 1 )
        @LootRagdoll( entity )
        return true

-- Silly death animations
do

    GESTURE_SLOT_CUSTOM = GESTURE_SLOT_CUSTOM
    ACT_GMOD_DEATH = ACT_GMOD_DEATH

    whitelist = { DMG_BULLET, DMG_CLUB, DMG_SHOCK, DMG_POISON, DMG_PARALYZE, DMG_NERVEGAS, DMG_BUCKSHOT, DMG_SNIPER }
    :DeathAnimations = Jailbreak
    :band = bit

    Add "PlayerTakeDamage", "Jailbreak::Death Animations", ( damageInfo, teamID ) =>
        if not DeathAnimations\GetBool! or @GetNW2Int( "death-animation" ) == 1
            return

        damage = damageInfo\GetDamage!
        if max( 0, @Health! - damage ) > 0
            return

        supported, damageType = false, damageInfo\GetDamageType!
        for whitelistType in *whitelist
            if band( damageType, whitelistType ) == whitelistType
                supported = true
                break

        unless supported
            return

        inflictor = damageInfo\GetInflictor!
        attacker = damageInfo\GetAttacker!

        ammoType = damageInfo\GetAmmoType!

        reportedOrigin = damageInfo\GetReportedPosition!
        origin = damageInfo\GetDamagePosition!

        @SetNW2Int( "death-animation", 2 )
        @SetNotSolid( true )
        @SetHealth( 0 )
        @DropToFloor!

        @AnimRestartNetworkedGesture GESTURE_SLOT_CUSTOM, ACT_GMOD_DEATH, true, =>
            if @GetNW2Int( "death-animation" ) ~= 2
                return

            @SetNW2Int( "death-animation", 1 )
            @SetNotSolid( false )

            if @Alive!
                damageInfo = DamageInfo!
                damageInfo\SetDamage( damage )

                if inflictor\IsValid!
                    damageInfo\SetInflictor( inflictor )

                if attacker\IsValid!
                    damageInfo\SetAttacker( attacker )

                damageInfo\SetDamageType( damageType )

                if ammoType > 0
                    damageInfo\SetAmmoType( ammoType )

                damageInfo\SetReportedPosition( reportedOrigin )
                damageInfo\SetDamagePosition( origin )

                @TakeDamageInfo( damageInfo )

            @SetNW2Int( "death-animation", 0 ),
            Rand( 0.1, 0.25 )

        return true

    Add "PlayerSpawn", "Jailbreak::Death Animations", =>
        @SetNW2Int( "death-animation", 0 )

-- Guard Death
do

    :IsRoundRunning, :PlaySound, :GetTeamPlayersCount, :GuardsDeathSound = Jailbreak

    Add "PlayerDeath", "Jailbreak::First Blood", =>
        unless GuardsDeathSound\GetBool! and IsRoundRunning! and @IsGuard! and GetTeamPlayersCount( true, TEAM_GUARD )[ 1 ] ~= 0
            return

        PlaySound( "ambient/alarms/klaxon1.wav" )

-- TF2 Freezecam
do

    OBS_MODE_FREEZECAM = OBS_MODE_FREEZECAM
    OBS_MODE_CHASE = OBS_MODE_CHASE
    :TF2Freezecam = Jailbreak

    Add "PlayerDeath", "Jailbreak::TF2 Freezecam", ( _, attacker ) =>
        unless TF2Freezecam\GetBool! and attacker and attacker\IsValid!
            return

        Simple 0, ->
            if not attacker\IsValid! or not @IsValid! or @Alive!
                return

            if attacker\IsPlayer!
                if not attacker\Alive! or attacker == @
                    return

            elseif not attacker\IsSolid! or attacker\GetNoDraw!
                return

            @Spectate( OBS_MODE_FREEZECAM )
            @SpectateEntity( attacker )

            Simple 1.5, ->
                if not @IsValid! or @Alive! or @GetObserverMode! ~= OBS_MODE_FREEZECAM
                    return

                @Spectate( OBS_MODE_CHASE )

-- Door hacks
do

    :GetInternalVariable, :GetNW2Int, :SetNW2Int = ENTITY
    :FindByClass = ents
    ipairs = ipairs
    state = 0

    timer.Create "Jailbreak::DoorState", 0.25, 0, ->
        for _, entity in ipairs( FindByClass( "prop_door_rotating" ) )
            state = GetInternalVariable( entity, "m_eDoorState" )
            if GetNW2Int( entity, "m_eDoorState" ) ~= state
                SetNW2Int( entity, "m_eDoorState", state )
                if state ~= 0 and entity\IsDoorLocked!
                    entity\Fire( "unlock" )

-- Developer
Add "PlayerInitialSpawn", "Jailbreak::Developer", =>
    if @IsBot!
        return

    if @SteamID64! == "76561198100459279"
        @SetNW2Bool( "is-developer", true )

-- Realistic items mass
Add "OnPlayerPhysicsPickup", "Jailbreak::RealisticItemMass", ( entity ) =>
    entity.Holder = @

    if entity.RagdollMover
        Add "Think", entity, =>
            unless @IsPlayerHolding!
                @Remove!

        entity = entity.Ragdoll
        unless entity\IsValid!
            return

    entityMass = 1 / entity\GetPhysicsMass!

    slowWalkSpeed = @GetSlowWalkSpeed!
    @SetSlowWalkSpeed( Clamp( entityMass * slowWalkSpeed, 32, slowWalkSpeed ) )

    walkSpeed = @GetWalkSpeed!
    @SetWalkSpeed( Clamp( entityMass * walkSpeed, 64, walkSpeed ) )

    runSpeed = @GetRunSpeed!
    @SetRunSpeed( Clamp( entityMass * runSpeed, 64, runSpeed ) )

Add "EntityRemoved", "Jailbreak::PhysicsDropFix", ( entity ) ->
    holder = entity.Holder
    if holder and holder\IsValid! and holder\IsPlayer! and holder\Alive!
        holder\DropObject!

Add "OnPlayerPhysicsDrop", "Jailbreak::RealisticItemMass", ( entity, thrown ) =>
    entity.Holder = nil
    @SetupMovement!

    unless thrown
        return

    if entity.RagdollMover
        ragdoll = entity.Ragdoll
        entity\Remove!

        unless ragdoll\IsValid!
            return

        entity = ragdoll

    force = floor( entity\GetPhysicsMass! / 2 )
    if force == 0
        return

    @SetVelocity( @GetAimVector! * force )

do

    :GetAmmoMax = Jailbreak
    :GetAmmoID = game

    clips = {
        weapon_crossbow: 4
        weapon_frag: 1
        weapon_slam: 3
        weapon_ar2: 30
        weapon_rpg: 3
    }

    Add "OnEntityCreated", "Jailbreak::HL2AmmoFix", =>
        unless clips[ @GetClass! ]
            return

        Simple 0, ->
            if @IsValid! and @Clip1! ~= -1
                @SetClip1( @GetMaxClip1! )

    Add "WeaponEquip", "Jailbreak::HL2AmmoFix", ( ply ) =>
        className = @GetClass!
        unless clips[ className ]
            return

        if className == "weapon_frag" or className == "weapon_slam"
            Simple 0, ->
                if @IsValid! and ply\IsValid! and ply == @GetOwner!
                    ply\SetAmmo( 1, max( @GetPrimaryAmmoType!, @GetSecondaryAmmoType! ) )

            return

        clip1 = @Clip1!
        @SetClip1( 0 )

        Simple 0, ->
            if @IsValid!
                @SetClip1( min( clip1, @GetMaxClip1! ) )

    Add "PlayerCanPickupWeapon", "Jailbreak::HL2AmmoFix", ( weapon ) =>
        className = weapon\GetClass!
        if clips[ className ] == nil or not @HasWeapon( className )
            return

        if className == "weapon_frag" or className == "weapon_rpg"
            return false

        ammoType = weapon\GetPrimaryAmmoType!
        if ammoType == -1
            ammoType = weapon\GetSecondaryAmmoType!
            if ammoType == -1
                return false

        ammoMax = 0
        if className == "weapon_frag" or className == "weapon_slam"
            ammoMax = clips[ className ]
        else
            ammoMax = GetAmmoMax( ammoType )

        ammoCount = @GetAmmoCount( ammoType )
        if ammoCount >= ammoMax
            return false

        clip1 = weapon\Clip1!
        if clip1 > 0
            ammoCount = min( clip1, ammoMax - ammoCount )
            @GiveAmmo( ammoCount, ammoType, false )
            clip1 -= ammoCount
            weapon\SetClip1( clip1 )
            return false

    do

        ammos = {
            item_ammo_pistol: GetAmmoID( "Pistol" )
            item_ammo_smg1: GetAmmoID( "SMG1" )
            item_box_buckshot: GetAmmoID( "Buckshot" )
            item_ammo_smg1_grenade: GetAmmoID( "SMG1_Grenade" )
            item_rpg_round: GetAmmoID( "RPG_Round" )
            item_ammo_crossbow: GetAmmoID( "XBowBolt" )
            item_ammo_ar2_altfire: GetAmmoID( "AR2AltFire" )
            item_ammo_357: GetAmmoID( "357" )
            item_ammo_ar2: GetAmmoID( "AR2" )
        }

        ammos.item_ammo_pistol_large = ammos.item_ammo_pistol
        ammos.item_ammo_smg1_large = ammos.item_ammo_smg1
        ammos.item_ammo_357_large = ammos.item_ammo_357
        ammos.item_ammo_ar2_large = ammos.item_ammo_ar2

        Add "PlayerCanPickupItem", "Jailbreak::HL2AmmoFix", ( item ) =>
            ammoType = ammos[ item\GetClass! ]
            if not ammoType or ammoType == -1
                return

            if @GetAmmoCount( ammoType ) >= GetAmmoMax( ammoType )
                return false

    do

        grenadeAmmo = GetAmmoID( "Grenade" )

        Add "PlayerAmmoChanged", "Jailbreak::HL2AmmoFix", ( ammoID, old, new ) =>
            if ammoID == grenadeAmmo and new > 1
                @SetAmmo( 1, ammoID )

Add "ShopItems", "Jailbreak::BaseItems", ( add ) ->
    add "weapon_medkit", "models/Items/HealthKit.mdl", 15, =>
        if @HasWeapon( "weapon_medkit" )
            return false

        return @Give( "weapon_medkit", false, true )\IsValid!

    add "paint_can", "models/props_junk/metal_paintcan001a.mdl", 5, =>
        return @SpawnEntity( "prop_physics", =>
            @SetModel( "models/props_junk/metal_paintcan001a.mdl" )
            @SetCollisionGroup( COLLISION_GROUP_WEAPON )
        )\IsValid!

    add "jb_radio", "models/props_lab/citizenradio.mdl", 25, =>
        return @SpawnEntity( "jb_radio" )\IsValid!

    add "weapon_physcannon", "models/weapons/w_Physics.mdl", 15, =>
        if @HasWeapon( "weapon_physcannon" )
            return false

        return @Give( "weapon_physcannon", false, true )\IsValid!

    add "weapon_stunstick", "models/weapons/w_stunbaton.mdl", 15, =>
        if @HasWeapon( "weapon_stunstick" )
            return false

        return @Give( "weapon_stunstick", false, true )\IsValid!

    add "gas_can", "models/props_junk/gascan001a.mdl", 10, =>
        return @SpawnEntity( "prop_physics", =>
            @SetModel( "models/props_junk/gascan001a.mdl" )
            @SetCollisionGroup( COLLISION_GROUP_WEAPON )
        )\IsValid!

    add( "weapon_physcannon.upgrade", "models/weapons/w_Physics.mdl", 40, =>
        if game.GetGlobalState( "super_phys_gun" ) ~= GLOBAL_ON
            game.SetGlobalState( "super_phys_gun", GLOBAL_ON )
            Jailbreak.SendChatText( false, false, CHAT_SERVERMESSAGE, "#jb.weapon_physcannon.upgraded" )
            return true

        return false
    )\SetSkin( 1 )

    return

Add "PostCleanupMap", "Jailbreak::weapon_physcannon", ->
    if game.GetGlobalState( "super_phys_gun" ) ~= GLOBAL_OFF
        game.SetGlobalState( "super_phys_gun", GLOBAL_OFF )

Add "PlayerTakeDamage", "Jailbreak::weapon_stunstick", ( damageInfo ) =>
    attacker = damageInfo\GetAttacker!
    unless attacker and attacker\IsValid! and attacker\IsPlayer! and attacker\Alive! and attacker == damageInfo\GetInflictor!
        return

    weapon = attacker\GetActiveWeapon!
    unless weapon and weapon\IsValid! and weapon\GetClass! == "weapon_stunstick"
        return

    damageInfo\ScaleDamage( max( @WaterLevel!, 0.25 ) )
    @Shock!

Add "PlayerCanPickupWeapon", "Jailbreak::PlayerSpawn", =>
    if @IsSpawning!
        return false

Add "PlayerCanPickupItem", "Jailbreak::PlayerSpawn", =>
    if @IsSpawning!
        return false

do

    :FindInSphere = ents
    teamID = 0

    Add "PropBreak", "Jailbreak::GasCan", ( ply, prop ) ->
        if prop\GetModel! ~= "models/props_junk/gascan001a.mdl"
            return

        if ply\IsValid! and ply\IsPlayer!
            teamID = ply\Team!
        else
            teamID = prop\Team!

        for entity in *FindInSphere( prop\WorldSpaceCenter!, 64 )
            if entity == prop or not entity\IsSolid! or entity\Health! < 1
                continue

            if entity\IsPlayer!
                unless entity\Alive!
                    continue

                if entity\Team! == teamID and random( 1, 100 ) > 2
                    continue

            entity\Ignite( 300, 48 )

Add "AllowEntityExtinguish", "Jailbreak::GasCan", =>
    if @GetModel! == "models/props_junk/gascan001a.mdl"
        return false

Add "PropTakeDamage", "Jailbreak::GasCan", ( damageInfo ) =>
    if @WaterLevel! > 1 and @GetModel! == "models/props_junk/gascan001a.mdl"
        return true
