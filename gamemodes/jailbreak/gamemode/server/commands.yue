Jailbreak = Jailbreak

:HasWarden, :Emotion = Jailbreak
NOTIFY_ERROR = NOTIFY_ERROR
CHAN_STATIC = CHAN_STATIC
tonumber = tonumber
IsValid = IsValid
:Add = concommand
CurTime = CurTime
:random = math

do

    :ChangeTeam = Jailbreak

    Add "changeteam", ( _, args ) =>
        teamID = args[ 1 ]
        if teamID
            teamID = tonumber( teamID ) or 0
        else
            teamID = 0

        ChangeTeam( @, teamID )

do

    :IsRoundRunning, :SetShockCollars, :IsShockCollarsActive = Jailbreak

    Add "jb_warden", ( _, args ) =>
        unless IsValid( @ )
            return

        unless IsRoundRunning! and @IsGuard! and @Alive!
            @SendNotify( "#jb.error.warden-failure", NOTIFY_ERROR, 10 )
            return

        if ( @WardenDelay or 0 ) > CurTime!
            @SendNotify( "#jb.please-wait", NOTIFY_ERROR, 3 )
            return

        if @IsWarden!
            @WardenDelay = CurTime! + 10
            @SetWarden( false )
            return

        if HasWarden!
            @SendNotify( "#jb.error.warden-exists", NOTIFY_ERROR, 10 )
            return

        @WardenDelay = CurTime! + 5
        @SetWarden( true )

    Add "jb_shock_collars", ( _, args ) =>
        unless IsValid( @ )
            return

        unless IsRoundRunning! and @Alive! and @IsWarden!
            @SendNotify( "#jb.error.cant-do-that", NOTIFY_ERROR, 10 )
            return

        if ( @ShockCollarsDelay or 0 ) > CurTime!
            @SendNotify( "#jb.please-wait", NOTIFY_ERROR, 3 )
            return

        requested = args[ 1 ]
        if requested ~= nil and #requested ~= 0
            SetShockCollars( requested == "1" )
        else
            SetShockCollars( not IsShockCollarsActive! )

        @ShockCollarsDelay = CurTime! + 3

do

    :SetRoundState, :SetRoundTime = Jailbreak
    :Clamp = math

    Add "jb_force_round", ( _, args ) =>
        if IsValid( @ ) and not @IsSuperAdmin!
            return

        index = args[ 1 ]
        if index
            SetRoundState( Clamp( tonumber( index ) or 0, 0, 3 ) )
        else
            SetRoundState( 0 )
            SetRoundTime( 0 )

Add "jb_respawn", ( _, args ) =>
    if IsValid( @ ) and not @IsSuperAdmin!
        return

    if #args == 0
        @Spawn!
        return

    index = tonumber( args[ 1 ] )
    unless index
        return

    ply = Entity( index )
    if ply\IsValid!
        ply\Spawn!

Add "jb_move_player", ( _, args ) =>
    if IsValid( @ ) and not @IsSuperAdmin!
        return

    index = tonumber( args[ 1 ] )
    unless index
        return

    ply = Entity( index )
    if ply and ply\IsValid!
        if ply\Alive!
            ply\Kill!

        teamID = tonumber( args[ 2 ] )
        if teamID
            ply\SetTeam( teamID )

        if args[ 3 ]
            ply\Spawn!

Add "jb_kick_player", ( _, args ) =>
    if IsValid( @ ) and not @IsSuperAdmin!
        return

    index = tonumber( args[ 1 ] )
    unless index
        return

    ply = Entity( index )
    if ply\IsValid!
        reason = args[ 2 ]
        if reason ~= nil
            ply\Kick( reason )
        else
            ply\Kick!

do

    :IsValidModel = util

    dropWeapon = =>
        unless @Alive!
            return

        droppedWeapon = @GetActiveWeapon!
        unless IsValid( droppedWeapon )
            return

        model = droppedWeapon\GetWeaponWorldModel!
        unless model and #model ~= 0 and IsValidModel( model )
            return

        traceResult, isPlayer = @GetEyeTrace!, false
        if traceResult.Hit
            entity = traceResult.Entity
            if entity\IsValid! and entity\IsPlayer! and entity\Alive!
                isPlayer = true

        @DropWeapon( droppedWeapon )

        unless droppedWeapon\IsInWorld!
            @PickupWeapon( droppedWeapon )
            @SelectWeapon( droppedWeapon\GetClass! )
            return

        if isPlayer
            @DoAnimationEvent( ACT_GMOD_GESTURE_ITEM_GIVE )
        else
            @DoAnimationEvent( ACT_GMOD_GESTURE_ITEM_DROP )

        maxWeight, nextWeapon = nil, nil
        for weapon in *@GetWeapons!
            weight = weapon\GetWeight!

            if not maxWeight or maxWeight <= weight
                nextWeapon = weapon
                maxWeight = weight

        unless IsValid( nextWeapon )
            return

        @SelectWeapon( nextWeapon\GetClass! )

    Add "drop", dropWeapon
    Jailbreak.SetChatCommand "drop", dropWeapon, "#jb.chat.command.drop"
    Jailbreak.SetChatCommand "dropweapon", dropWeapon, "#jb.chat.command.drop"

do

    util.AddNetworkString( "Jailbreak::Markers" )

    :Markers, :VoiceChatMinDistance, :MarkersLifetime, :MarkersCount, :GetWeaponName = Jailbreak
    :Start, :WriteEntity, :WriteBool, :WriteVector, :Send, :Broadcast = net
    :white = Jailbreak.Colors
    EmitSound = EmitSound
    :TraceLine = util
    :abs = math

    trace = {
        mask: MASK_SHOT
    }

    Add "marker", =>
        unless Markers\GetBool! and IsValid( @ ) and @Alive!
            return

        if @NextMarker and CurTime! < @NextMarker
            return

        isWarden, isPrisoner = @IsWarden!, @IsPrisoner!
        if not isWarden and @IsGuard! and not HasWarden!
            isWarden = true

        unless isWarden or isPrisoner
            @SendNotify( "#jb.error.cant-do-that", NOTIFY_ERROR, 10 )
            return

        distance = 0
        if isWarden
            distance = 32768
        elseif isPrisoner
            distance = 4096

        @LagCompensation( true )

        trace.filter = @

        start = @EyePos!
        trace.start = start
        trace.endpos = start + @GetAimVector! * distance

        traceResult = TraceLine( trace )

        @LagCompensation( false )

        unless traceResult.Hit
            return

        @NextMarker = CurTime! + ( MarkersLifetime\GetInt! / MarkersCount\GetInt! )

        entity = traceResult.Entity
        isValid = IsValid( entity )

        Start( "Jailbreak::Markers" )
        WriteEntity( @ )
        WriteBool( isValid )

        origin = nil
        if isValid
            WriteEntity( entity )
            origin = entity\WorldToLocal( traceResult.HitPos )
        else
            origin = traceResult.HitPos

        WriteVector( origin )

        if isWarden
            Broadcast!

            rf = RecipientFilter!
            rf\AddPAS( origin )

            if isValid
                entity\EmitSound( "buttons/button" .. random( 14, 19 ) .. ".wav", 75, random( 80, 120 ), 1, CHAN_STATIC, 0, 1, rf )
            else
                EmitSound( "buttons/button" .. random( 14, 19 ) .. ".wav", origin, 0, CHAN_STATIC, 1, 75, 0, random( 80, 120 ), 1, rf )

        elseif isPrisoner
            rf = RecipientFilter!

            hasSender = false
            for ply in *@GetNearPlayers( VoiceChatMinDistance\GetInt!, true )
                if ply == @
                    hasSender = true

                rf\AddPlayer( ply )

            unless hasSender
                rf\AddPlayer( @ )

            Send( rf )

            if isValid
                entity\EmitSound( "buttons/button" .. random( 14, 19 ) .. ".wav", 75, random( 80, 120 ), 1, CHAN_STATIC, 0, 1, rf )
            else
                EmitSound( "buttons/button" .. random( 14, 19 ) .. ".wav", origin, 0, CHAN_STATIC, 1, 75, 0, random( 80, 120 ), 1, rf )

        if isValid
            @AnimRestartNetworkedGesture( GESTURE_SLOT_CUSTOM, ACT_SIGNAL_GROUP, true )
        else
            @AnimRestartNetworkedGesture( GESTURE_SLOT_CUSTOM, ACT_SIGNAL_FORWARD, true )

        if entity\IsValid!
            if entity\IsPlayer!
                Emotion( @, "#jb.chat.pointed-at \"", entity\GetModelColor!, entity\Nick!, white, "\"." )
            elseif entity\IsPlayerRagdoll!
                Emotion( @, "#jb.chat.pointed-at \"", entity\GetModelColor!, entity\GetRagdollOwnerNickname!, white, "\"." )
            elseif entity\IsWeapon!
                Emotion( @, "#jb.chat.pointed-at \"" .. GetWeaponName( entity ) .. "\"." )
            else
                Emotion( @, "#jb.chat.pointed-at \"#jb." .. entity\GetClass! .. "\"." )

        elseif traceResult.HitSky
            Emotion( @, "#jb.chat.pointed-at \"#jb.sky\"." )

        else

            dir = traceResult.HitNormal
            if abs( dir[ 1 ] ) > 0.5 or abs( dir[ 2 ] ) > 0.5
                Emotion( @, "#jb.chat.pointed-at \"#jb.wall\"." )
            elseif dir[ 3 ] > 0.5
                Emotion( @, "#jb.chat.pointed-at \"#jb.floor\"." )
            else
                Emotion( @, "#jb.chat.pointed-at \"#jb.ceiling\"." )

Add "jb_reload_localization", =>
    if IsValid( @ ) and not @IsListenServerHost!
        return

    Jailbreak.ReloadLocalization!

do

    :ShopItems, :TakeWardenCoins, :CanWardenAfford = Jailbreak
    NOTIFY_GENERIC = NOTIFY_GENERIC
    :Run = hook

    Add "jb_buy", ( _, args ) =>
        unless IsValid( @ ) and @Alive! and @IsWarden!
            return

        item = ShopItems[ args[ 1 ] ]
        unless item
            return

        unless CanWardenAfford( item.price )
            @SendNotify( "#jb.shop.not-enough-coins", NOTIFY_ERROR, 5 )
            return

        if Run( "Jailbreak::PlayerCanBuyItem", @, item ) == false
            @SendNotify( "#jb.error.cant-do-that", NOTIFY_ERROR, 5 )
            return

        if item.action( @, item ) == false
            @SendNotify( "#jb.error.cant-do-that", NOTIFY_ERROR, 5 )
            return

        TakeWardenCoins( item.price )

        @EmitSound( "ambient/levels/labs/coinslot1.wav", 75, random( 80, 120 ), 1, CHAN_STATIC, 0, 1 )
        @SendNotify( "#jb.shop.you-bought \"" .. item.title .. "\"", NOTIFY_GENERIC, 5 )
        Emotion( @, "#jb.chat.bought \"" .. item.title .. "\"." )
        Run( "Jailbreak::PlayerBoughtItem", @, item )
