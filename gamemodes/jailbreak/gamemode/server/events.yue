ErrorNoHaltWithStack = ErrorNoHaltWithStack
:random, :Clamp, :Rand = math
hook_Remove = hook.Remove
Jailbreak = Jailbreak
tostring = tostring
hook_Add = hook.Add
xpcall = xpcall
:gsub = string

ROUND_WAITING_PLAYERS = ROUND_WAITING_PLAYERS
ROUND_RUNNING = ROUND_RUNNING
ROUND_FINISHED = ROUND_FINISHED

:Colors, :SendChatText, :GetTeamPlayers, :IsRoundRunning = Jailbreak
CHAT_SERVERMESSAGE = CHAT_SERVERMESSAGE
CHAT_CUSTOM = CHAT_CUSTOM
:GetAll = player
:white = Colors

events = Jailbreak.Events
unless istable( events )
    events = {}
    Jailbreak.Events = events

class JailbreakEvent
    ConVarFlags: bit.bor( FCVAR_ARCHIVE, FCVAR_NOTIFY )
    new: ( name, init, finish ) =>
        assert isstring( name ), "name must be a string"
        assert isfunction( init ), "init must be a function"

        @startState = ROUND_PREPARING
        @finishState = ROUND_RUNNING
        @type = "default"

        if isfunction( finish )
            @finish = finish

        @color = white
        @init = init
        @name = name

        events[] = @

    GetStartState: =>
        return @startState

    SetStartState: ( state ) =>
        @startState = state

    GetFinishState: =>
        return @finishState

    SetFinishState: ( state ) =>
        @finishState = state

    ConVar: ( default ) =>
        @SetChance( CreateConVar( "jb_event_" .. gsub( @name, "[%p%s]+", "_" ) .. "_chance", tostring( Clamp( default, 0, 100 ) ), @ConVarFlags, "Chance of '" .. @name .. "' event.", 0, 100 ) )

    GetChance: =>
        return @chance

    SetChance: ( chance ) =>
        if TypeID( chance ) ~= TYPE_CONVAR
            assert isnumber( chance ), "chance must be a number"
        @chance = chance

    GetType: =>
        return @type

    SetType: ( str ) =>
        assert isstring( str ), "type must be a string"
        @type = str

    GetColor: =>
        return @color

    SetColor: ( color ) =>
        assert IsColor( color ), "color must be a Color"
        @color = color

    GetMessage: =>
        return @color, "#jb.event." .. @name

    SendMessage: =>
        SendChatText( false, false, CHAT_CUSTOM, white, "#jb.round.modifier.added: ", @GetMessage! )

    Finish: ( state ) =>
        if state ~= @finishState
            return

        finish = @finish
        if finish
            xpcall( finish, ErrorNoHaltWithStack, @, state )

    Run: ( state ) =>
        if state ~= @startState
            return

        chance = @chance
        unless chance
            return

        if TypeID( chance ) == TYPE_CONVAR
            chance = chance\GetInt!

        if isnumber( chance ) and ( ( chance == 0 ) or ( chance ~= 100 and random( 1, 100 ) > chance ) )
            return

        status, writeMessage, stopEvents = xpcall( @init, ErrorNoHaltWithStack, @, state )
        if status and writeMessage
            @SendMessage!

        return stopEvents or false

Jailbreak.RegisterEvent = ( name, init, finish ) ->
    for event in *events
        if event.name == name
            event.init = init
            event.finish = finish
            return event

    return JailbreakEvent( name, init, finish )

Jailbreak.RunEvents = ( state ) ->
    stoppedTypes = {}
    for event in *events
        event\Finish( state )

        if stoppedTypes[ event.type ]
            continue

        if event\Run( state )
            stoppedTypes[ event.type ] = true

-- Female prison
do

    event = Jailbreak.RegisterEvent "female-prison", ->
            Jailbreak.SetFemalePrison( true )
            return true, true,
        ->
            Jailbreak.SetFemalePrison( false )

    event\SetFinishState( ROUND_FINISHED )
    event\SetType( "playermodel" )
    event\SetColor( Colors.pink )
    event\ConVar( 15 )

-- Masquerade
do

    :PrecacheModel = util
    Vector = Vector
    :Random = table

    Models = player_manager.AllValidModels!

    event = Jailbreak.RegisterEvent "masquerade", ->
            hook_Add "PlayerSetModel", "Jailbreak::MasqueradeEvent", =>
                modelPath = Random( Models )
                if modelPath ~= nil
                    modelPath = Jailbreak.FixModelPath( modelPath )
                    PrecacheModel( modelPath )
                    @SetModel( modelPath )
                    return true

            hook_Add "TeamPlayerSpawn", "Jailbreak::MasqueradeEvent", =>
                if @IsBot!
                    return

                @SetPlayerColor( Vector( Rand( 0, 1 ), Rand( 0, 1 ), Rand( 0, 1 ) ) )
                @SetWeaponColor( Vector( Rand( 0, 1 ), Rand( 0, 1 ), Rand( 0, 1 ) ) )
                @SetSkin( random( 0, @SkinCount! ) )

                for bodygroup in *@GetBodyGroups!
                    @SetBodygroup( bodygroup.id, random( 0, bodygroup.num - 1 ) )

            return true, true,
        ->
            hook_Remove "TeamPlayerSpawn", "Jailbreak::MasqueradeEvent"
            hook_Remove "PlayerSetModel", "Jailbreak::MasqueradeEvent"

    event\SetFinishState( ROUND_FINISHED )
    event\SetType( "playermodel" )
    event\ConVar( 5 )

    HSVToColor = HSVToColor

    event.SendMessage = =>
        seed = random( 0, 360 )

        for ply in *player.GetHumans!
            text = Jailbreak.GetPhrase( ply, "jb.event." .. @name )
            message, index = {}, 0

            for i = 1, utf8.len( text )
                index += 1
                message[ index ] = HSVToColor( i * seed % 360, 1, 1 )

                index += 1
                message[ index ] = utf8.sub( text, i, i )

            SendChatText( ply, false, CHAT_CUSTOM, white, "#jb.round.modifier.added: ", unpack( message ) )

-- Grass is lava
do

    event = Jailbreak.RegisterEvent "grass-is-lava", ->
            hook_Add "PlayerDeathSound", "Jailbreak::GrassIsLavaEvent", =>
                @EmitSound( "vo/npc/" .. ( Jailbreak.IsFemalePrison! and "female01" or "male01" ) .. "/ohno.wav", 90, random( 80, 120 ), 1, CHAN_STATIC, 0, 1 )
                return true

            hook_Add "PlayerFootstep", "Jailbreak::GrassIsLavaEvent", ( pos, _, __, volume ) =>
                unless @Alive!
                    return

                pos[ 3 ] -= 1000
                eyePos = @EyePos!

                traceResult = util.TraceLine( {
                    start: eyePos
                    endpos: pos
                    filter: @
                } )

                unless traceResult.Hit and traceResult.HitWorld
                    return

                if traceResult.MatType == MAT_GRASS
                    @SetColor( Colors.black )
                    @Ignite( 0.25, 32 )

                    damageInfo = DamageInfo!
                    damageInfo\SetDamageType( DMG_BURN )
                    damageInfo\SetDamage( @Health! + @Armor! )
                    damageInfo\SetDamageForce( ( eyePos - pos ) * 10 )
                    damageInfo\SetDamagePosition( pos )
                    damageInfo\SetAttacker( @ )
                    @TakeDamageInfo( damageInfo )
                    return true

            return true, false,
        ->
            hook_Remove "PlayerDeathSound", "Jailbreak::GrassIsLavaEvent"
            hook_Remove "PlayerFootstep", "Jailbreak::GrassIsLavaEvent"

    event\SetStartState( ROUND_RUNNING )
    event\SetFinishState( ROUND_FINISHED )
    event\SetType( "death" )
    event\ConVar( 5 )

    event.SendMessage = =>
        for ply in *player.GetHumans!
            text = Jailbreak.GetPhrase( ply, "jb.event." .. @name )
            message, index = {}, 0

            length = utf8.len( text )
            for i = 1, length
                index += 1
                message[ index ] = HSVToColor( ( length - i ) * 10 % 360, 1, 1 )

                index += 1
                message[ index ] = utf8.sub( text, i, i )

            SendChatText( ply, false, CHAT_CUSTOM, white, "#jb.round.modifier.added: ", unpack( message ) )

-- Powerful players
do

    event = Jailbreak.RegisterEvent "powerful-players", ->
            Jailbreak.PowerfulPlayers = true

            for ply in *GetAll!
                if ply\Alive!
                    ply\AnimRestartNetworkedGesture( GESTURE_SLOT_CUSTOM, ACT_GMOD_GESTURE_TAUNT_ZOMBIE, true )

            return true, false,
        ->
            Jailbreak.PowerfulPlayers = false

    event\SetStartState( ROUND_RUNNING )
    event\SetFinishState( ROUND_FINISHED )
    event\SetColor( Colors.red )
    event\SetType( "damage" )
    event\ConVar( 5 )

-- Random knife
do

    TEAM_PRISONER = TEAM_PRISONER
    timer_Simple = timer.Simple
    :ceil = math

    event = Jailbreak.RegisterEvent "random-knife", ->
        timer_Simple 0, ->
            unless IsRoundRunning!
                return

            prisoners = GetTeamPlayers( true, TEAM_PRISONER )[ 1 ]
            prisonerCount = #prisoners
            if prisonerCount == 0
                return

            for i = 1, ceil( prisonerCount * 0.25 )
                ply = prisoners[ random( 1, prisonerCount ) ]
                unless ply and ply\IsValid!
                    continue

                unless ply\HasWeapon( "weapon_knife" )
                    ply\Give( "weapon_knife" )

    event\SetStartState( ROUND_RUNNING )
    event\SetFinishState( nil )
    event\SetType( "weapon" )
    event\ConVar( 10 )

-- Hidden Angel
do

    event = Jailbreak.RegisterEvent "hidden-angel", ->
            prisoners = GetTeamPlayers( true, TEAM_PRISONER )[ 1 ]

            prisonerCount = #prisoners
            if prisonerCount == 0
                return false, false

            ply = prisoners[ random( 1, prisonerCount ) ]
            ply\AllowFlight( true )
            SendChatText( ply, false, CHAT_SERVERMESSAGE, "#jb.event.hidden-angel.message" )
            return true, true,
        ->
            for ply in *GetAll!
                ply\AllowFlight( false )

    event\SetStartState( ROUND_RUNNING )
    event\SetFinishState( ROUND_WAITING_PLAYERS )
    event\SetColor( Colors.dark_white )
    event\SetType( "movement" )
    event\ConVar( 10 )
