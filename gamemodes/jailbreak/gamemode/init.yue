AddCSLuaFile( "shared.lua" )
include( "shared.lua" )
Jailbreak = Jailbreak

global NOTIFY_GENERIC = 0
global NOTIFY_ERROR = 1
global NOTIFY_UNDO = 2
global NOTIFY_HINT = 3
global NOTIFY_CLEANUP = 4

ROUND_WAITING_PLAYERS = ROUND_WAITING_PLAYERS
ROUND_PREPARING = ROUND_PREPARING
ROUND_RUNNING = ROUND_RUNNING
ROUND_ENDED = ROUND_ENDED

TEAM_PRISONER = TEAM_PRISONER
TEAM_GUARD = TEAM_GUARD

ConVarFlags = bit.bor( FCVAR_ARCHIVE, FCVAR_NOTIFY )
CreateConVar = CreateConVar

GM.PrepareTime = CreateConVar( "jb_prepare_time", "10", ConVarFlags, "The time before the start of the round that is given for preparation.", 5, 2 * 60 * 60 )
GM.RoundTime = CreateConVar( "jb_round_time", "0", ConVarFlags, "Round time in seconds.", 0, 12 * 60 * 60 )

GM.PlayerRespawnTime = CreateConVar( "jb_respawn_time", "2", ConVarFlags, "Time to respawn players after death during preparation.", 0, 300 )
GM.DoorsHealth = CreateConVar( "jb_doors_health", "500", ConVarFlags, "The value is responsible for the health of the door.", 50, 10000 )
GM.GuardsArmor = CreateConVar( "jb_guards_armor", "0", ConVarFlags, "The value of the guards' armor when spawning.", 0, 1000 )
GM.WeaponThrowing = CreateConVar( "jb_weapon_throwing", "0", ConVarFlags, "Allow to pick up weapons as a prop, provided that the player has a full ammo.", 0, 1 )
GM.ManualWeaponPickup = CreateConVar( "jb_manula_weapon_pickup", "0", ConVarFlags, "You can only pick up weapons with the use key.", 0, 1 )

GM.FemalePrisonChance = CreateConVar( "jb_female_prison_chance", "10", ConVarFlags, "The percentage value at which a women's prison will appear where all inmates have female models.", 0, 100 )
GM.MasqueradeEventChance = CreateConVar( "jb_masquerade_event_chance", "1", ConVarFlags, "The percentage of probability that the next round will be a masquerade..", 0, 100 )
GM.PrisonerKnifeChance = CreateConVar( "jb_prisoner_knife_chance", "10", ConVarFlags, "Percentage value at which a prisoner can have a knife on spawn.", 0, 100 )

Classic = Jailbreak.Classic\GetBool!
RunConsoleCommand( "mp_show_voice_icons", Classic and "1" or "0" )
cvars.AddChangeCallback( "jb_classic", ( _, __, value ) ->
    RunConsoleCommand( "mp_show_voice_icons", value )
    Classic = value == "1"
"Jailbreak" )

Jailbreak.GuardsDiff = 1 / ( CreateConVar( "jb_prisoner_per_guard", "3", ConVarFlags, "Number of prisoners per guard.", 1, game.MaxPlayers! - 1 )\GetInt! + 1 )
cvars.AddChangeCallback( "jb_prisoner_per_guard", ( _, __, value ) ->
    Jailbreak.GuardsDiff = 1 / ( ( tonumber( value ) or 3 ) + 1 )
"Jailbreak" )

Jailbreak.VoiceChatPVS = CreateConVar( "jb_voice_chat_pvs", "0", ConVarFlags, "Uses PVS instead of PAS.", 0, 1 )

util.AddNetworkString( "Jailbreak::Networking" )

resource.AddWorkshop( "3093227948" )
resource.AddWorkshop( "2950445307" )
resource.AddWorkshop( "2661291057" )
resource.AddWorkshop( "643148462" )

math, table = math, table
:Rand, :random, :max, :min = math
:Effect, :IsValidModel, :IsInWorld, :TraceHull, :ScreenShake = util
timer_Simple = timer.Simple
ents_Create = ents.Create
:Empty, :Random = table
hook_Run = hook.Run
tonumber = tonumber
istable = istable
CurTime = CurTime
IsValid = IsValid
string = string

do

    SetGlobal2Bool = SetGlobal2Bool
    SetGlobal2Int = SetGlobal2Int

    Jailbreak.SetRoundState = ( state, silent ) ->
        oldState = Jailbreak.GetRoundState!
        SetGlobal2Int( "round-state", state )
        unless silent
            hook_Run( "RoundStateChanged", oldState, state )

    Jailbreak.SetWinningTeam = ( teamID ) ->
        if Jailbreak.Teams[ teamID ]
            team.AddScore( teamID, 1 )
        SetGlobal2Int( "winning-team", teamID )

    Jailbreak.SetRoundTime = ( int ) ->
        SetGlobal2Int( "next-round-state", CurTime! + int )

    Jailbreak.SetFemalePrison = ( bool ) ->
        if bool ~= Jailbreak.IsFemalePrison!
            SetGlobal2Bool( "female-prison", bool )

    Jailbreak.SetMasqueradeEvent = ( bool ) ->
        if bool ~= Jailbreak.IsMasqueradeEvent!
            SetGlobal2Bool( "masquerade-event", bool )

    Jailbreak.SetElectroCollars = ( bool, silent ) ->
        if bool == Jailbreak.IsElectroCollarsActive!
            return

        SetGlobal2Bool( "electro-collars", bool )
        unless silent
            hook_Run( "ElectroCollars", bool )

    Jailbreak.SetWardenCoins = ( value, silent ) ->
        oldValue = Jailbreak.GetWardenCoins!
        if value == oldValue
            return

        SetGlobal2Int( "warden-coins", value )
        unless silent
            hook_Run( "WardenCoins", oldValue, value )

    Jailbreak.BroadcastNotify = ( text, typeID, length, ... ) ->
        net.Start( "Jailbreak::Networking" )
        net.WriteUInt( 2, 2 )
        net.WriteString( text )
        net.WriteTable( { ... }, true )
        net.WriteUInt( typeID, 3 )
        net.WriteUInt( length, 16 )
        net.Broadcast!

    Jailbreak.SendChatText = ( target, ... ) ->
        net.Start( "Jailbreak::Networking" )
        net.WriteUInt( 3, 2 )
        net.WriteTable( { ... }, true )
        if target
            net.Send( target )
        else
            net.Broadcast!

do

    ENTITY = FindMetaTable( "Entity" )

    ENTITY.SetCreator = ( entity ) =>
        @SetNW2Entity( "entity-owner", entity )

do

    PLAYER = FindMetaTable( "Player" )

    PLAYER.SendPickupNotify = ( itemName ) =>
        net.Start( "Jailbreak::Networking" )
        net.WriteUInt( 1, 2 )
        net.WriteString( itemName )
        net.Send( @ )

    PLAYER.SendNotify = ( text, typeID, length, ... ) =>
        net.Start( "Jailbreak::Networking" )
        net.WriteUInt( 2, 2 )
        net.WriteString( text )
        net.WriteTable( { ... }, true )
        net.WriteUInt( typeID, 3 )
        net.WriteUInt( length, 16 )
        net.Send( @ )

    PLAYER.SetWarden = ( bool, silent ) =>
        if bool == @IsWarden!
            return

        @SetNW2Bool( "is-warden", bool )
        if bool and not silent
            hook_Run( "CreatedWarden", @ )

    PLAYER.SetElectroCollar = ( bool ) =>
        @SetNW2Bool( "electro-collar", bool )

    PLAYER.AllowSecurityAccess = ( bool ) =>
        @SetNW2Bool( "security-access", bool )

    PLAYER.AllowSecurityRadio = ( bool ) =>
        @SetNW2Bool( "security-radio", bool )

    PLAYER.MoveSpectateID = ( move, players ) =>
        index, length = ( @LastSpectatedEntity or 0 ) + move, #players
        if index > length
            index = 1
        elseif index < 1
            index = length

        entity = players[ index ]
        if entity\IsPlayer!
            @SpectateEntity( entity )
            if @GetObserverMode! ~= OBS_MODE_CHASE
                @Spectate( OBS_MODE_CHASE )
        else
            if @GetObserverMode! ~= OBS_MODE_ROAMING
                @Spectate( OBS_MODE_ROAMING )
            @SetPos( entity\GetPos! )
            angles = entity\GetAngles!
            angles[ 3 ] = 0
            @SetEyeAngles( angles )
        @LastSpectatedEntity = index

    PLAYER.UsingSecurityRadio = =>
        return @HasSecurityRadio! and @GetInfo( "cl_security_radio" ) == "1"

    do

        WeaponHandlers = WeaponHandlers or {}

        PLAYER.Give = ( className, noAmmo ) =>
            weapon = nil

            handler = WeaponHandlers[ className ]
            if handler
                weapon = ents_Create( handler.Alternative or className )
            else
                weapon = ents_Create( className )

            unless weapon and weapon\IsValid!
                return

            weapon\SetAngles( @GetAngles! )
            weapon\SetPos( @GetPos! )

            weapon\Spawn!
            weapon\Activate!

            if weapon\IsWeapon!
                if noAmmo
                    weapon\SetClip1( 0 )
                    weapon\SetClip2( 0 )
                @PickupWeapon( weapon, false )
            return weapon

    PLAYER.SetMutedByWarden = ( bool ) =>
        @SetNW2Bool( "warden-mute", bool )

    PLAYER.RemoveRagdoll = =>
        ragdoll = @GetRagdollEntity!
        if ragdoll\IsValid!
            ragdoll\Remove!

    PLAYER.CreateClientsideRagdoll = PLAYER.CreateClientsideRagdoll or PLAYER.CreateRagdoll
    COLLISION_GROUP_PASSABLE_DOOR = COLLISION_GROUP_PASSABLE_DOOR

    PLAYER.CreateRagdoll = =>
        @RemoveRagdoll!

        modelPath = @GetModel!
        unless IsValidModel( modelPath )
            return NULL

        spawnOrigin = @WorldSpaceCenter!
        unless IsInWorld( spawnOrigin )
            return NULL

        ragdoll = ents_Create( @GetBoneCount! > 1 and "prop_ragdoll" or "prop_physics" )
        ragdoll\SetAngles( @GetAngles! )
        ragdoll\SetModel( modelPath )
        ragdoll\SetPos( spawnOrigin )
        ragdoll\Spawn!

        Jailbreak.PlayerRagdolls[] = ragdoll
        ragdoll.Team = @Team!

        if @Alive!
            ragdoll.Alive = true
            ragdoll.PlayerAngles = @EyeAngles!

            -- TODO: Custom take damage
            ragdoll\SetMaxHealth( @GetMaxHealth! )
            ragdoll\SetHealth( @Health! )

            ragdoll.MaxArmor = @GetMaxArmor!
            ragdoll.Armor = @Armor!
        else
            ragdoll.Alive = false

        for bodygroup in *@GetBodyGroups!
            ragdoll\SetBodygroup( bodygroup.id, @GetBodygroup( bodygroup.id ) )

        ragdoll\SetFlexScale( @GetFlexScale! )
        for flexID = 0, @GetFlexNum!
            ragdoll\SetFlexWeight( flexID, @GetFlexWeight( flexID ) )

        ragdoll\SetPlayerColor( @GetPlayerColor! )
        ragdoll\SetMaterial( @GetMaterial! )
        ragdoll\SetColor( @GetColor! )
        ragdoll\SetSkin( @GetSkin! )

        for index = 1, #@GetMaterials!
            materialPath = @GetSubMaterial( index )
            if materialPath ~= ""
                ragdoll\SetSubMaterial( index, materialPath )

        ragdoll\SetCollisionGroup( COLLISION_GROUP_PASSABLE_DOOR )
        ragdoll\SetNW2Bool( "is-player-ragdoll", true )
        ragdoll\SetNW2Entity( "ragdoll-owner", @ )
        @SetNW2Entity( "player-ragdoll", ragdoll )

        if @IsBot!
            ragdoll\SetNW2String( "steamid", @SteamID64! )
        ragdoll\SetNW2String( "nickname", @Nick! )
        ragdoll\SetCreator( @ )

        for boneID = 0, @GetBoneCount!
            ragdoll\ManipulateBonePosition( boneID, @GetManipulateBonePosition( boneID ) )
            ragdoll\ManipulateBoneAngles( boneID, @GetManipulateBoneAngles( boneID ) )
            ragdoll\ManipulateBoneJiggle( boneID, @GetManipulateBoneJiggle( boneID ) )
            ragdoll\ManipulateBoneScale( boneID, @GetManipulateBoneScale( boneID ) )

        if ragdoll\IsRagdoll!
            velocity = @LastMoveVelocity
            for physNum = 0, ragdoll\GetPhysicsObjectCount! - 1 do
                phys = ragdoll\GetPhysicsObjectNum( physNum )
                unless IsValid( phys )
                    continue

                boneID = ragdoll\TranslatePhysBoneToBone( physNum )
                if boneID < 0
                    continue

                origin, angles = @GetBonePosition( boneID )
                phys\SetVelocity( velocity )
                phys\SetAngles( angles )
                phys\SetPos( origin )
                phys\Wake!

        else
            phys = ragdoll\GetPhysicsObject!
            if IsValid( phys )
                phys\SetVelocity( @LastMoveVelocity )
                phys\Wake!

        if @IsOnFire!
            ragdoll\Ignite( 16, 64 )
            @Extinguish!

        hook_Run( "PlayerRagdollCreated", @, ragdoll )

        ragdoll.HasSecurityAccess = @HasSecurityAccess!
        ragdoll.HasSecurityRadio = @HasSecurityRadio!
        ragdoll.HasFlashlight = @CanUseFlashlight!

        weapons = {}
        for weapon in *@GetWeapons!
            @DropWeapon( weapon )

            weapon\SetPos( spawnOrigin )
            weapon\SetParent( ragdoll )
            weapon\SetNotSolid( true )
            weapon\SetNoDraw( true )

            weapon.PickupForbidden = true
            weapons[] = weapon
        ragdoll.Weapons = weapons
        ragdoll.Ammo = @GetAmmo!

        return ragdoll

GM.CreatedWarden = ( ply ) =>
    Jailbreak.BroadcastNotify( "#jb.alert.new_warden", NOTIFY_HINT, 20, ply\Nick! )

GM.ElectroCollarEffect = ( ply ) =>
    headBone, origin = ply\LookupBone( "ValveBiped.Bip01_Head1" ), nil
    if headBone and headBone >= 0
        matrix = ply\GetBoneMatrix( headBone )
        if matrix
            origin = matrix\GetTranslation!

    fx = EffectData!
    fx\SetScale( 0.5 )
    fx\SetOrigin( origin or ply\EyePos! )
    fx\SetMagnitude( random( 3, 5 ) )
    fx\SetRadius( random( 1, 5 ) )
    Effect( "ElectricSpark", fx )

    ply\EmitSound( "Jailbreak.Shock" )

GM.ElectroCollars = ( bool ) =>
    for ply in *player.GetAll!
        if ply\Alive! and ply\HasElectroCollar!
            @ElectroCollarEffect( ply )
            ply\SendNotify( "#jb.notify.electro_collar." .. ( bool and "on" or "off" ), bool and NOTIFY_GENERIC or NOTIFY_UNDO, 30 )

GM.ShowTeam = ( ply ) =>
    ply\ConCommand( "jb_showteam" )

Jailbreak.PlayerRagdolls = Jailbreak.PlayerRagdolls or {}

GM.PlayerInitialSpawn = ( ply, transiton ) =>
    ply.NextRespawnTime = 0
    isBot = ply\IsBot!

    sid64 = isBot and ply\Nick! or ply\SteamID64!
    for ragdoll in *Jailbreak.PlayerRagdolls
        if sid64 == ( isBot and ragdoll\GetNW2String( "nickname" ) or ragdoll\GetNW2String( "steamid" ) )
            ply\SetNW2Entity( "player-ragdoll", ragdoll )
            break

    if isBot
        ply\SetTeam( TEAM_PRISONER )
    else
        ply\SetTeam( TEAM_SPECTATOR )
        ply.AvailableSpeakers = {}

    ragdoll = ply\GetRagdollEntity!
    if ragdoll\IsValid!
        ply\SetTeam( ragdoll.Team )

        unless ragdoll.Alive and ragdoll\Health! > 1
            ply\KillSilent!
            return

        ply\SetEyeAngles( Angle( 0, ragdoll.PlayerAngles[ 2 ], 0 ) )
        ply\SetPos( ragdoll\WorldSpaceCenter! )
        ply\SetModel( ragdoll\GetModel! )

        ply\SetHealth( ply\GetMaxHealth! * ( ragdoll\Health! / ragdoll\GetMaxHealth! ) )
        ply\SetArmor( ply\GetMaxArmor! * ( ragdoll.Armor / ragdoll.MaxArmor ) )

        ply\SetMaterial( ragdoll\GetMaterial! )
        ply\SetColor( ragdoll\GetColor! )
        ply\SetSkin( ragdoll\GetSkin! )

        for bodygroup in *ply\GetBodyGroups!
            ply\SetBodygroup( bodygroup.id, ragdoll\GetBodygroup( bodygroup.id ) )

        ply\SetFlexScale( ragdoll\GetFlexScale! )
        for flexID = 0, ply\GetFlexNum!
            ply\SetFlexWeight( flexID, ragdoll\GetFlexWeight( flexID ) )

        for index = 1, #ply\GetMaterials!
            materialPath = ragdoll\GetSubMaterial( index )
            if materialPath ~= ""
                ply\SetSubMaterial( index, materialPath )

        if ragdoll\IsOnFire!
            ply\Ignite( 16, 64 )
            ragdoll\Extinguish!

        for boneID = 0, ply\GetBoneCount!
            ply\ManipulateBonePosition( boneID, ragdoll\GetManipulateBonePosition( boneID ) )
            ply\ManipulateBoneAngles( boneID, ragdoll\GetManipulateBoneAngles( boneID ) )
            ply\ManipulateBoneJiggle( boneID, ragdoll\GetManipulateBoneJiggle( boneID ) )
            ply\ManipulateBoneScale( boneID, ragdoll\GetManipulateBoneScale( boneID ) )

        if ragdoll\IsRagdoll!
            velocity = Vector!

            count = ragdoll\GetPhysicsObjectCount!
            for physNum = 0, count - 1
                phys = ragdoll\GetPhysicsObjectNum( physNum )
                if IsValid( phys )
                    velocity += phys\GetVelocity!

            velocity /= count
            ply\SetVelocity( velocity )
        else
            phys = ragdoll\GetPhysicsObject!
            if IsValid( phys )
                ply\SetVelocity( phys\GetVelocity! )

        hook_Run( "PlayerRagdollUse", ragdoll, ply )
        ragdoll\Remove!

GM.PlayerSlowWalkSpeed = CreateConVar( "jb_player_slow_walk_speed", "100", ConVarFlags, "The speed of the player during a slow stride.", 0, 3000 )
GM.PlayerWalkSpeed = CreateConVar( "jb_player_walk_speed", "185", ConVarFlags, "The speed of the player while walking.", 0, 3000 )
GM.PlayerRunSpeed = CreateConVar( "jb_player_run_speed", "365", ConVarFlags, "The speed of the player while running.", 0, 3000 )

GM.PlayerSpawn = ( ply, transiton ) =>
    ply\DisableWorldClicking( true )
    ply\SetCanZoom( false )
    ply\SetWarden( false )
    ply\RemoveRagdoll!
    ply\RemoveAllAmmo!
    ply\StripWeapons!
    ply\UnSpectate!

    ply\SetSlowWalkSpeed( @PlayerSlowWalkSpeed\GetInt! )
    ply\SetWalkSpeed( @PlayerWalkSpeed\GetInt! )
    ply\SetRunSpeed( @PlayerRunSpeed\GetInt! )

    ply\SetMaxHealth( 100 )
    ply\SetMaxArmor( 100 )
    ply\SetHealth( 100 )

    teamID = ply\Team!
    if teamID == TEAM_GUARD
        ply\SetArmor( @GuardsArmor\GetInt! )
    else
        ply\SetArmor( 0 )

    ply\SetMaxArmor( max( 100, ply\Armor! ) )

    isPrisoner = teamID == TEAM_PRISONER
    ply\SetElectroCollar( isPrisoner )

    if @MapGame == "tf"
        ply\SetNoCollideWithTeammates( true )
        ply\SetAvoidPlayers( true )
    elseif isPrisoner
        ply\SetNoCollideWithTeammates( false )
        ply\SetAvoidPlayers( true )
    else
        ply\SetNoCollideWithTeammates( true )
        ply\SetAvoidPlayers( false )

    if teamID == TEAM_GUARD
        ply\AllowSecurityAccess( true )
        ply\AllowSecurityRadio( true )
        ply\AllowFlashlight( true )
    else
        ply\AllowSecurityAccess( false )
        ply\AllowSecurityRadio( false )
        ply\AllowFlashlight( false )

    unless Jailbreak.Teams[ teamID ]
        ply\KillSilent!
        return

    unless transiton
        hook_Run( "PlayerLoadout", ply, teamID )
    hook_Run( "PlayerSetModel", ply, teamID )

    if ply\IsBot! or Jailbreak.IsMasqueradeEvent!
        ply\SetPlayerColor( Vector( Rand( 0, 1 ), Rand( 0, 1 ), Rand( 0, 1 ) ) )
        ply\SetWeaponColor( Vector( Rand( 0, 1 ), Rand( 0, 1 ), Rand( 0, 1 ) ) )
        ply\SetSkin( random( 0, ply\SkinCount! ) )

        for bodygroup in *ply\GetBodyGroups!
            ply\SetBodygroup( bodygroup.id, random( 0, bodygroup.num - 1 ) )

    else
        ply\SetPlayerColor( Vector( ply\GetInfo( "cl_playercolor" ) ) )
        ply\SetSkin( ply\GetInfoNum( "cl_playerskin", 0 ) )

        weaponColor = Vector( ply\GetInfo( "cl_weaponcolor" ) )
        if weaponColor\Length! < 0.001
            weaponColor = Vector( 0.001, 0.001, 0.001 )
        ply\SetWeaponColor( weaponColor )

        groups = string.Explode( " ", ply\GetInfo( "cl_playerbodygroups" ) or "" )
        for i = 0, ply\GetNumBodyGroups! - 1 do
            ply\SetBodygroup( i, tonumber( groups[ i + 1 ] ) or 0 )

    hook_Run( "TeamPlayerSpawn", ply, teamID )
    ply\SetupHands!

GM.OnEntityCreated = ( weapon ) =>
    if weapon\GetClass! == "game_player_equip"
        weapon\Remove!
        return

    unless weapon\IsWeapon!
        return

    timer_Simple 0.025, ->
        unless weapon\IsValid!
            return

        if IsValid( weapon\GetOwner! )
            return

        if weapon\GetPos!\Length! <= 3
            weapon\Remove!
            return

        weapon\SetCollisionGroup( COLLISION_GROUP_WEAPON )

        phys = weapon\GetPhysicsObject!
        unless IsValid( phys ) and phys\IsMotionEnabled!
            return

        counter = 0
        for entity in *ents.FindInSphere( weapon\GetPos!, 32 )
            if entity\IsWeapon! and not IsValid( entity\GetOwner! )
                counter = counter + 1
                if counter >= 5
                    phys\EnableMotion( false )
                    return

COLLISION_GROUP_DEBRIS = COLLISION_GROUP_DEBRIS

GM.PlayerDroppedWeapon = ( ply, weapon ) =>
    timerName = "JB_WeaponDrop #" .. weapon\EntIndex!
    timer.Create( timerName, 3, 1, ->
        timer.Remove( timerName )
        unless weapon\IsValid!
            return

        phys = weapon\GetPhysicsObject!
        unless IsValid( phys )
            return

        start = weapon\WorldSpaceCenter!
        mins, maxs = weapon\GetCollisionBounds!

        tr = TraceHull( {
            start: start
            endpos: start + Vector( 0, 0, mins[ 3 ] - 2 )
            mask: MASK_SOLID_BRUSHONLY
            mins: mins
            maxs: maxs
        } )

        if tr.Hit
            weapon\SetCollisionGroup( COLLISION_GROUP_DEBRIS )
            phys\EnableMotion( false )
            return

        @PlayerDroppedWeapon( ply, weapon )
    )

GM.PlayerLoadout = ( ply, teamID ) =>
    if teamID == TEAM_GUARD
        ply\GiveAmmo( 7, "357", true )
        ply\GiveAmmo( 60, "ar2", true )
        ply\GiveAmmo( 90, "smg1", true )
        ply\GiveAmmo( 40, "Pistol", true )
        ply\GiveAmmo( 20, "Buckshot", true )

        if @MapGame ~= "cstrike"
            avaliableWeapons = @AvaliableWeapons
            count = #avaliableWeapons / 4
            gived = {}

            while count > 0
                className = Random( avaliableWeapons, true )
                if ply\HasWeapon( className ) or gived[ className ]
                    continue

                gived[ className ] = true
                ply\Give( className )
                count -= 1
    elseif teamID == TEAM_PRISONER
        unless ply\HasWeapon( "weapon_knife" )
            if Classic
                ply\Give( "weapon_knife" )
                return
            elseif random( 1, 100 ) <= @PrisonerKnifeChance\GetInt!
                ply\Give( "weapon_knife" )

    ply\Give( "jb_hands" )
    ply\SelectWeapon( "jb_hands" )

do

    :PrecacheModel = util

    GM.PlayerSetModel = ( ply, teamID ) =>
        if Jailbreak.IsMasqueradeEvent!
            modelPath = table.Random( player_manager.AllValidModels! )
            if modelPath ~= nil
                PrecacheModel( modelPath )
                ply\SetModel( modelPath )
                return

        requested = Jailbreak.FixModelPath( player_manager.TranslatePlayerModel( ply\GetInfo( "cl_playermodel" ) ) )
        if Jailbreak.CustomPlayerModels\GetBool! and IsValidModel( requested )
            PrecacheModel( requested )
            ply\SetModel( requested )
            return

        models = Jailbreak.PlayerModels[ teamID ]
        if teamID == TEAM_PRISONER
            models = models[ Jailbreak.IsFemalePrison! ]

        if IsValidModel( requested )
            requestedName = string.match( requested, "([%w%_%-]+)%.mdl$" )
            for modelPath in *models
                if modelPath == requested or string.match( modelPath, "([%w%_%-]+)%.mdl$" ) == requestedName
                    ply\SetModel( modelPath )
                    return

        ply\SetModel( Random( models ) )

GM.PlayerSetHandsModel = ( ply, hands ) =>
    info = player_manager.TranslatePlayerHands( player_manager.TranslateToPlayerModelName( ply\GetModel! ) )
    unless info
        return

    hands\SetModel( info.model )
    hands\SetBodyGroups( info.body )
    hands\SetPlayerColor( ply\GetPlayerColor! )
    hands\SetSkin( info.matchBodySkin and ply\GetSkin! or info.skin )

GM.IsSpawnpointSuitable = ( ply, entity, makeSuitable ) =>
    if entity\GetClass! == "info_player_teamspawn"
        if entity.Disabled
            return false

        if entity.Team % 2 + 1 ~= ply\Team!
            return false

    start = entity\GetPos!
    mins, maxs = ply\GetHull!

    traceResult = TraceHull( {
        start: start
        endpos: start
        mask: MASK_PLAYERSOLID
        filter: entity
        mins: mins
        maxs: maxs
    } )

    if traceResult.Hit
        if makeSuitable
            traceEntity = traceResult.Entity
            if traceEntity\IsValid! and traceEntity\IsPlayer! and traceEntity\Alive!
                traceEntity\Kill!
                return true
        return false
    return true

do

    :IsEmpty = table

    GM.PlayerSelectTeamSpawn = ( teamID, ply ) =>
        spawnPoints = team.GetSpawnPoints( teamID )
        unless spawnPoints and not IsEmpty( spawnPoints )
            return

        for i = 1, 128
            spawnPoint = Random( spawnPoints )
            if hook_Run( "IsSpawnpointSuitable", ply, spawnPoint, i > 16 )
                return spawnPoint

GM.DoPlayerDeath = ( ply, attacker, damageInfo ) =>
    teamID = ply\Team!
    unless Jailbreak.Teams[ teamID ]
        return

    ply\AddDeaths( 1 )

    ragdoll = ply\CreateRagdoll!
    if ragdoll\IsValid!

        damageOrigin = damageInfo\GetDamagePosition!
        if ragdoll\IsRagdoll!
            velocity = damageInfo\GetDamageForce!
            for physNum = 0, ragdoll\GetPhysicsObjectCount! - 1 do
                phys = ragdoll\GetPhysicsObjectNum( physNum )
                if IsValid( phys )
                    phys\ApplyForceOffset( velocity / phys\GetMass!, damageOrigin )

        else
            phys = ragdoll\GetPhysicsObject!
            if IsValid( phys )
                phys\ApplyForceOffset( damageInfo\GetDamageForce! / phys\GetMass!, damageOrigin )

        timer_Simple 0, ->
            unless ply\IsValid! and ragdoll\IsValid!
                return

            ply\Spectate( OBS_MODE_CHASE )
            ply\SpectateEntity( ragdoll )

    hook_Run( "TeamPlayerDeath", ply, teamID )

do

    MovementKeys = bit.bor( IN_FORWARD, IN_BACK, IN_MOVELEFT, IN_MOVERIGHT, IN_JUMP )

    GM.SpectatorThink = ( ply ) =>
        observerMode = ply\GetObserverMode!
        if observerMode ~= OBS_MODE_ROAMING and observerMode ~= OBS_MODE_CHASE
            ply\Spectate( OBS_MODE_ROAMING )
        else
            if ply\KeyPressed( MovementKeys )
                if observerMode ~= OBS_MODE_ROAMING
                    ply\Spectate( OBS_MODE_ROAMING )
                    ply\SpectateEntity!
            elseif ply\KeyPressed( IN_ATTACK )
                targets = {}
                for pl in *player.GetAll!
                    if pl\IsInGame!
                        targets[] = pl

                for entity in *ents.FindByClass( "info_observer_point" )
                    targets[] = entity

                if #targets == 0
                    return

                ply\MoveSpectateID( 1, targets )
            elseif ply\KeyPressed( IN_ATTACK2 )
                targets = {}
                for pl in *player.GetAll!
                    if pl\IsInGame!
                        targets[] = pl

                for entity in *ents.FindByClass( "info_observer_point" )
                    targets[] = entity

                if #targets == 0
                    return

                ply\MoveSpectateID( -1, targets )

GM.PlayerDeathThink = ( ply ) =>
    if not Jailbreak.Teams[ ply\Team! ] or Jailbreak.GameInProgress!
        @SpectatorThink( ply )
        return

    if ply.NextRespawnTime > CurTime!
        return

    ply\Spawn!

GM.PreCleanupMap = =>
    Empty( Jailbreak.PlayerRagdolls )

do

    :RemoveByValue = table

    GM.EntityRemoved = ( entity, fullUpdate ) =>
        if fullUpdate
            return

        if entity\IsPlayerRagdoll!
            RemoveByValue( Jailbreak.PlayerRagdolls, entity )

GM.PlayerDisconnected = ( ply ) =>
    teamID = ply\Team!
    if Jailbreak.Teams[ teamID ]
        hook_Run( "TeamPlayerDisconnected", ply, teamID )

DMG_NEVERGIB = DMG_NEVERGIB

do

    GM.CloseRangeDamageTypes = { DMG_SLASH, DMG_FALL, DMG_CLUB, DMG_CRUSH }
    GM.ElectroCollarVictimDamage = CreateConVar( "jb_electro_collar_victim_damage", "0.25", ConVarFlags, "Damage to the victim from the electric collar.", 0, 1000 )
    GM.ElectroCollarAttackerDamage = CreateConVar( "jb_electro_collar_attacker_damage", "0.5", ConVarFlags, "Damage to the attacker from the electric collar.", 0, 1000 )

    GM.PlayerTakeDamage = ( ply, damageInfo, teamID ) =>
        damageType = damageInfo\GetDamageType!
        if ply\HasElectroCollar! and bit.band( damageType, DMG_SHOCK ) == DMG_SHOCK
            ply\SetElectroCollar( false )

        unless Jailbreak.IsElectroCollarsActive!
            return

        attacker = damageInfo\GetAttacker!
        if attacker\IsValid! and attacker\IsPlayer! and attacker\HasElectroCollar! and attacker ~= ply
            for int in *@CloseRangeDamageTypes
                if bit.band( damageType, int ) == int
                    damage = damageInfo\GetDamage!

                    newDamage = damage * @ElectroCollarAttackerDamage\GetFloat!
                    if newDamage >= 1
                        @ElectroCollarEffect( attacker )
                        damageInfo\SetDamage( newDamage )
                        damageInfo\SetDamageType( DMG_SONIC )
                        attacker\TakeDamageInfo( damageInfo )

                    newDamage = damage * @ElectroCollarVictimDamage\GetFloat!
                    if newDamage < 1
                        return true

                    damageInfo\SetDamageType( damageType )
                    damageInfo\SetDamage( newDamage )
                    break

GM.EntityTakeDamage = ( entity, damageInfo ) =>
    if entity\IsPlayerRagdoll!
        return true

    if entity\IsPlayer!
        if entity\Alive!
            teamID = entity\Team!
            if hook_Run( "CanPlayerTakeDamage", entity, damageInfo, teamID ) ~= false
                return hook_Run( "PlayerTakeDamage", entity, damageInfo, teamID )
        return true

    if bit.band( damageInfo\GetDamageType!, DMG_NEVERGIB ) == DMG_NEVERGIB
        return

    switch entity\GetClass!
        when "prop_door_rotating"
            maxHealth = entity\GetMaxHealth!
            if maxHealth <= 1
                maxHealth = @DoorsHealth\GetInt!
                if maxHealth <= 0
                    return true

                entity\SetHealth( maxHealth )
                entity\SetMaxHealth( maxHealth )

            health = entity\Health!
            if health < 1
                return true

            health = max( 0, health - damageInfo\GetDamage! )
            entity\SetHealth( health )

            if health > 1
                return true

            model = entity\GetModel!
            unless model and #model ~= 0 and IsValidModel( model )
                return true

            center = entity\OBBCenter!
            center[ 1 ] = 0
            center[ 2 ] = 0

            origin = entity\LocalToWorld( center )
            unless IsInWorld( origin )
                return true

            prop = ents_Create( "prop_physics" )
            prop\SetCollisionGroup( COLLISION_GROUP_WEAPON )
            prop\SetAngles( entity\GetAngles! )
            prop\SetSkin( entity\GetSkin! )
            prop\SetModel( model )
            prop\SetPos( origin )
            prop\Spawn!

            name = entity\GetName!
            if name ~= ""
                for portal in *ents.FindByClass( "func_areaportal" )
                    if portal\GetInternalVariable( "target" ) ~= name
                        continue

                    portal\SetSaveValue( "target", "" )
                    portal\Fire( "open" )

            prop\EmitSound( "physics/wood/wood_crate_break" .. random( 1, 5 ) .. ".wav", 70, random( 80, 120 ), 1, CHAN_STATIC, 0, 1 )
            entity\Remove!

            phys = prop\GetPhysicsObject!
            if IsValid( phys )
                phys\ApplyForceOffset( damageInfo\GetDamageForce!, damageInfo\GetDamagePosition! )

            origin = damageInfo\GetDamagePosition!
            fx = EffectData!
            fx\SetOrigin( origin )
            fx\SetMagnitude( 5 )
            fx\SetRadius( 5 )
            fx\SetScale( 2 )
            Effect( "Sparks", fx )
            ScreenShake( origin, 5, 10, 0.5, 150 )
            return true
        when "func_button"
            if bit.band( damageInfo\GetDamageType!, DMG_BLAST ) == DMG_BLAST
                return true

            attacker = damageInfo\GetAttacker!
            if IsValid( attacker ) and attacker\IsPlayer!
                entity\Use( attacker, attacker )
            return true

    if entity\GetNoDraw! or not entity\IsSolid!
        return

    velocity = damageInfo\GetDamageForce!
    for physNum = 0, entity\GetPhysicsObjectCount! - 1 do
        phys = entity\GetPhysicsObjectNum( physNum )
        if IsValid( phys )
            phys\ApplyForceOffset( velocity, damageInfo\GetDamagePosition! )

do

    :ceil = math

    GM.GetFallDamage = ( ply, speed ) =>
        return max( 0, ceil( 0.2418 * speed - 141.75 ) )

GM.PlayerShouldTaunt = ( ply ) =>
    return ply\IsInGame!

GM.AllowPlayerPickup = ( ply, entity ) =>
    return ply\IsInGame!

GM.PlayerCanPickupItem = ( ply, entity ) =>
    return ply\IsInGame!

GM.PlayerCanPickupWeapon = ( ply, weapon ) =>
    if @ManualWeaponPickup\GetBool! or ply\HasWeapon( weapon\GetClass! )
        return false
    return ply\Alive!

GM.PlayerNoClip = ( ply, desiredState ) =>
    if desiredState
        return ply\IsInGame! and ply\IsSuperAdmin! and not ply\IsPlayingTaunt!
    return true

GM.CanPlayerSuicide = ( ply ) =>
    if Jailbreak.IsRoundPreparing!
        return false
    return ply\IsInGame!

GM.PlayerSwitchFlashlight = ( ply ) =>
    return ( Classic or ply\CanUseFlashlight! ) and ply\Alive!

GM.PlayerDeathSound = ( ply ) =>
    if Classic or ply\IsGuard!
        ply\EmitSound( "Player.Death", 75, random( 80, 120 ), 1, CHAN_VOICE, 0, 1 )
    return true

GM.PlayerRagdollUse = ( ragdoll, ply ) =>
    velocity = Vector!

    direction = ply\EyePos! - ragdoll\WorldSpaceCenter!
    direction\Normalize!

    unless Classic
        if ragdoll.HasFlashlight and not ply\CanUseFlashlight!
            velocity += direction * 150
            ply\AllowFlashlight( true )
            ply\SendPickupNotify( "jb.flashlight" )
            ragdoll.HasFlashlight = nil

        if ragdoll.HasSecurityAccess and not ply\HasSecurityAccess!
            velocity += direction * 50
            ply\AllowSecurityAccess( true )
            ply\SendPickupNotify( "jb.access-card" )
            ragdoll.HasSecurityAccess = nil

        if ragdoll.HasSecurityRadio and not ply\HasSecurityRadio!
            velocity += direction * 50
            ply\AllowSecurityRadio( true )
            ply\SendPickupNotify( "jb.walkie-talkie" )
            ragdoll.HasSecurityRadio = nil

    weapons = ragdoll.Weapons
    if istable( weapons )
        for weapon in *weapons
            unless weapon\IsValid! and not IsValid( weapon\GetOwner! )
                continue

            weapon\SetNotSolid( false )
            weapon\SetNoDraw( false )
            weapon\SetParent!

            weapon.PickupForbidden = nil
            ply\PickupWeapon( weapon )
            velocity += direction * 200
    ragdoll.Weapons = nil

    ammo = ragdoll.Ammo
    if istable( ammo )
        for ammoType, ammoCount in pairs( ammo )
            ply\GiveAmmo( ammoCount, ammoType )
            velocity += direction * 100
    ragdoll.Ammo = nil

    if velocity\Length! < 100
        return

    if ragdoll\IsRagdoll!
        physParts = {}
        for physNum = 1, ragdoll\GetPhysicsObjectCount! - 1 do
            phys = ragdoll\GetPhysicsObjectNum( physNum )
            if IsValid( phys )
                physParts[] = phys

        table.Shuffle( physParts )

        for index = 1, min( #physParts, 4 )
            physParts[ index ]\ApplyForceCenter( velocity )

    else
        phys = ragdoll\GetPhysicsObject!
        if IsValid( phys )
            phys\ApplyForceCenter( velocity )

GM.PlayerSpray = ( ply ) =>
    return not ( ply\IsInGame! and ( Classic or ply\IsPrisoner! ) )

GM.PlayerUse = ( ply, entity ) =>
    unless ply\IsInGame!
        return

    unless Classic
        weapon = ply\GetActiveWeapon!
        if IsValid( weapon ) and weapon\GetHoldType! == "fist" and entity\IsPlayer!
            velocity = ply\GetVelocity!\GetNormalized! * 16
            entity\SetVelocity( velocity )
            ply\SetVelocity( -velocity )
            return false

    curTime, lastUse = CurTime!, ply.LastUse or 0
    ply.LastUse = curTime

    if ( curTime - lastUse ) <= 0.025
        return

    if entity\IsPlayerRagdoll!
        hook_Run( "PlayerRagdollUse", entity, ply )
        return false

    if entity\IsWeapon!
        if entity.PickupForbidden or entity.IsTemporaryWeapon or entity\IsPlayerHolding!
            return false

        unless ply\HasWeapon( entity\GetClass! )
            ply\PickupWeapon( entity, false )
            return false

        clip1, clip1Type = entity\Clip1!, entity\GetPrimaryAmmoType!
        if clip1 > 0 and clip1Type >= 0 and ply\GetAmmoCount( clip1Type ) < ( game.GetAmmoMax( clip1Type ) / 2 )
            ply\GiveAmmo( clip1, clip1Type, false )
            entity\SetClip1( 0 )
            return false

        clip2, clip2Type = entity\Clip2!, entity\GetSecondaryAmmoType!
        if clip2 > 0 and clip2Type >= 0 and ply\GetAmmoCount( clip2Type ) < ( game.GetAmmoMax( clip2Type ) / 2 )
            ply\GiveAmmo( clip2, clip2Type, false )
            entity\SetClip2( 0 )
            return false

        if @WeaponThrowing\GetBool!
            phys = entity\GetPhysicsObject!
            if IsValid( phys )
                phys\EnableMotion( true )
                phys\Wake!
            ply\PickupObject( entity )

            hook.Add "Think", entity, ->
                if entity\GetCollisionGroup! ~= COLLISION_GROUP_DEBRIS
                    entity\SetCollisionGroup( COLLISION_GROUP_DEBRIS )
                unless entity\IsPlayerHolding!
                    hook.Remove "Think", entity
            return false

    return true

do

    DistToSqr = FindMetaTable( "Vector" ).DistToSqr
    EyePos = FindMetaTable( "Entity" ).EyePos
    player_GetHumans = player.GetHumans
    RecipientFilter = RecipientFilter
    :Clamp, :Round = math
    net = net

    util.AddNetworkString( "JB::Voice Chat" )

    SendVolumes = ( listener, volumes ) ->
        net.Start( "JB::Voice Chat", true )
        net.WriteUInt( #volumes, 10 )
        for data in *volumes
            net.WriteEntity( data[ 1 ] )
            net.WriteFloat( data[ 2 ] )
        net.Send( listener )

    MinDistance = CreateConVar( "jb_voice_chat_distance_min", "64", ConVarFlags, "The minimum value at which the player can be heard.", 32, 16384 )\GetInt! ^ 2
    MaxDistance = CreateConVar( "jb_voice_chat_distance_max", "512", ConVarFlags, "The maximum value at which a player can be heard.", 128, 16384 )\GetInt! ^ 2

    cvars.AddChangeCallback( "jb_voice_chat_distance_min", ( _, __, value ) ->
        MinDistance = ( tonumber( value ) or 100 ) ^ 2
    "Jailbreak" )

    cvars.AddChangeCallback( "jb_voice_chat_distance_max", ( _, __, value ) ->
        MaxDistance = ( tonumber( value ) or 100 ) ^ 2
    "Jailbreak" )

    OOC = GetConVar( "sv_alltalk" )\GetInt! > 2
    cvars.AddChangeCallback( "sv_alltalk", ( _, __, value ) ->
        OOC = ( tonumber( value ) or 2 ) > 2
    "Jailbreak" )

    CanHear = ( listener, speaker ) ->
        if speaker\Alive!
            if speaker\IsMutedByWarden!
                return false, false
        else
            return not listener\Alive!, false

    timer.Create "JB:Voice Chat Think", 0.5, 0, ->
        players = player_GetHumans!
        for listener in *players
            speakers = listener.AvailableSpeakers
            Empty( speakers )
            volumes = {}

            if OOC or not Jailbreak.IsRoundRunning!
                for speaker in *players
                    volumes[] = { speaker, 1 }
                    speakers[ speaker ] = 1

                SendVolumes( listener, volumes )
                continue

            origin = EyePos( listener )

            rf = RecipientFilter!
            if Jailbreak.VoiceChatPVS\GetBool!
                rf\AddPVS( origin )
            else
                rf\AddPAS( origin )

            hasRadio = listener\HasSecurityRadio!
            listenerIndex = listener\EntIndex!

            for speaker in *rf\GetPlayers!
                if speaker\IsBot! or speaker\EntIndex! == listenerIndex
                    continue

                if CanHear( listener, speaker ) == false
                    continue

                if hasRadio and speaker\UsingSecurityRadio!
                    continue

                volume = Round( 1 - Clamp( max( 0, DistToSqr( origin, EyePos( speaker ) ) - MinDistance ) / MaxDistance, 0, 1 ), 2 )
                if volume > 0
                    volumes[] = { speaker, volume }
                    speakers[ speaker ] = volume

            if hasRadio
                for speaker in *players
                    if speaker\UsingSecurityRadio!
                        volumes[] = { speaker, 1 }
                        speakers[ speaker ] = 1

            SendVolumes( listener, volumes )

    GM.PlayerCanSeePlayersChat = ( _, isTeam, listener, speaker ) =>
        if listener\IsBot!
            return

        unless IsValid( speaker )
            return true

        if isTeam and listener\Team! ~= speaker\Team!
            return false

        if OOC or not Jailbreak.IsRoundRunning!
            return true

        canHear = CanHear( listener, speaker )
        if canHear ~= nil
            return canHear

        return listener.AvailableSpeakers[ speaker ] ~= nil

    GM.PlayerCanHearPlayersVoice = ( listener, speaker ) =>
        if listener\EntIndex! == speaker\EntIndex! or listener\IsBot! or speaker\IsBot!
            return

        if OOC or not Jailbreak.IsRoundRunning!
            return true, false

        canHear, is3D = CanHear( listener, speaker )
        if canHear ~= nil
            return canHear, is3D

        if listener.AvailableSpeakers[ speaker ] ~= nil
            return true, not speaker\UsingSecurityRadio!

        return false, false

GM.GetTeamAliveCount = ( teamID ) =>
    alive = 0
    for ply in *team.GetPlayers( teamID )
        if ply\Alive!
            alive += 1
    return alive

GM.PlayerRequestTeam = ( ply, teamID ) =>
    unless team.Joinable( teamID )
        return

    oldTeamID = ply\Team!
    if oldTeamID == teamID
        return

    if teamID == TEAM_GUARD
        guardCount = @GetTeamAliveCount( TEAM_GUARD )
        if guardCount ~= 0 and ( guardCount / max( 1, @GetTeamAliveCount( TEAM_PRISONER ) ) ) >= Jailbreak.GuardsDiff
            ply\SendNotify( "#jb.error.too_many_guards", NOTIFY_ERROR, 10 )
            return

    if ply\Alive!
        if Jailbreak.Teams[ oldTeamID ]
            ply\Kill!
        else
            ply\KillSilent!

    ply\SetTeam( teamID )

concommand.Add( "drop", ( ply ) ->
    dropWeapon = ply\GetActiveWeapon!
    unless IsValid( dropWeapon )
        return

    model = dropWeapon\GetWeaponWorldModel!
    unless model and IsValidModel( model )
        return

    ply\DropWeapon( dropWeapon )

    maxWeight, nextWeapon = nil, nil
    for weapon in *ply\GetWeapons!
        weight = weapon\GetWeight!

        if not maxWeight or maxWeight <= weight
            nextWeapon = weapon
            maxWeight = weight

    unless IsValid( nextWeapon )
        return

    ply\SelectWeapon( nextWeapon\GetClass! )
)

GM.InitPostEntity = =>
    RunConsoleCommand( "sv_defaultdeployspeed", "1" )
    Jailbreak.SetRoundState( ROUND_WAITING_PLAYERS )

    if #ents.FindByClass( "info_player_teamspawn" ) > 0
        resource.AddWorkshop( "105181283" )
        SoundHandler( "tf" )
        @MapGame = "tf"
    elseif ( #ents.FindByClass( "info_player_counterterrorist" ) + #ents.FindByClass( "info_player_counterterrorist" ) ) > 0
        SoundHandler( "cstrike" )
        @MapGame = "cstrike"

GM.PlayerInitialized = ( ply ) =>
    gameName = @MapGame
    unless gameName
        return

    net.Start( "Jailbreak::Networking" )
    net.WriteUInt( 0, 2 )
    net.WriteString( gameName )
    net.Send( ply )

GM.SetupMove = ( ply, _, cmd ) =>
    if ply\IsFullyConnected! or not cmd\IsForced!
        return

    ply\SetNW2Bool( "fully-connected", true )
    hook_Run( "PlayerInitialized", ply )

GM.Think = =>
    if Jailbreak.GetRoundTime! <= CurTime!
        switch Jailbreak.GetRoundState!
            when ROUND_PREPARING
                Jailbreak.SetRoundState( ROUND_RUNNING )
            when ROUND_RUNNING
                if @RoundTime\GetInt! == 0
                    return

                team.AddScore( TEAM_GUARD, 1 )
                Jailbreak.SetWinningTeam( TEAM_GUARD )

                Jailbreak.SetRoundState( ROUND_ENDED, true )
                Jailbreak.SetRoundTime( @PrepareTime\GetInt! )
            when ROUND_ENDED
                Jailbreak.SetRoundState( ROUND_WAITING_PLAYERS )

GM.RoundStateChanged = ( old, new ) =>
    switch ( new )
        when ROUND_WAITING_PLAYERS
            Jailbreak.SetElectroCollars( false )

            for ply in *player.GetAll!
                if ply\IsBot!
                    ply\SetTeam( TEAM_PRISONER )
                elseif Jailbreak.Teams[ ply\Team! ]
                    ply\KillSilent!

    		timer_Simple( 0, game.CleanUpMap )

        when ROUND_PREPARING
            Jailbreak.SetElectroCollars( true )

            if random( 1, 100 ) <= @FemalePrisonChance\GetInt!
                Jailbreak.SetFemalePrison( true )
            elseif random( 1, 100 ) <= @MasqueradeEventChance\GetInt!
                Jailbreak.SetMasqueradeEvent( true )

            for ply in *player.GetAll!
                if ply\IsBot!
                    ply\SetTeam( TEAM_PRISONER )

                if Jailbreak.Teams[ ply\Team! ]
                    ply\Spawn!

    		timer_Simple( 0, game.CleanUpMap )

        when ROUND_RUNNING
            Jailbreak.SetElectroCollars( true )

            guardCount, prisonerCount = team.NumPlayers( TEAM_GUARD ), team.NumPlayers( TEAM_PRISONER )
            totalCount = guardCount + prisonerCount

            while ( guardCount / prisonerCount ) > Jailbreak.GuardsDiff and totalCount > ( ( 1 / Jailbreak.GuardsDiff ) + guardCount )
                for i = 1, guardCount
                    guard = Random( team.GetPlayers( TEAM_GUARD ), true )
                    if guard\IsValid!
                        guard\SetTeam( TEAM_PRISONER )
                        guard\Spawn!
                        break

                guardCount, prisonerCount = team.NumPlayers( TEAM_GUARD ), team.NumPlayers( TEAM_PRISONER )
                totalCount = guardCount + prisonerCount

            if guardCount == 0 or prisonerCount == 0
                Jailbreak.SetRoundState( ROUND_WAITING_PLAYERS )
                return

            for ply in *player.GetAll!
                if ply\IsBot!
                    guardCount = team.NumPlayers( TEAM_GUARD )
                    if guardCount == 0 or ( ( guardCount + 1 ) / team.NumPlayers( TEAM_PRISONER ) ) < Jailbreak.GuardsDiff
                        ply\SetTeam( TEAM_GUARD )
                    else
                        ply\SetTeam( TEAM_PRISONER )
                    ply\Spawn!
                elseif Jailbreak.Teams[ ply\Team! ] and not ply\Alive!
                    ply\Spawn!

            guards = team.GetPlayers( TEAM_GUARD )
            if #guards == 1
                guards[ 1 ]\SetWarden( true )

            roundTime = @RoundTime\GetInt!
            if roundTime > 0
                Jailbreak.SetRoundTime( roundTime )

        when ROUND_ENDED
            guardCount, prisonerCount = @GetTeamAliveCount( TEAM_GUARD ), @GetTeamAliveCount( TEAM_PRISONER )
            if guardCount > prisonerCount
                Jailbreak.SetWinningTeam( TEAM_GUARD )
            elseif guardCount < prisonerCount
                Jailbreak.SetWinningTeam( TEAM_PRISONER )
            else
                Jailbreak.SetWinningTeam( 0 )
            Jailbreak.SetRoundTime( @PrepareTime\GetInt! )
            Jailbreak.SetMasqueradeEvent( false )
            Jailbreak.SetFemalePrison( false )

GM.TeamPlayerDeath = ( ply, teamID ) =>
    ply.NextRespawnTime = CurTime! + @PlayerRespawnTime\GetFloat!

    ply\AllowSecurityAccess( false )
    ply\AllowSecurityRadio( false )
    ply\SetElectroCollar( false )
    ply\AllowFlashlight( false )
    ply\SetWarden( false )
    ply\UnSpectate!

    timer.Create "Jailbreak::TeamPlayerDeath", 0.5, 1, ->
        timer.Remove "Jailbreak::TeamPlayerDeath"
        unless Jailbreak.IsRoundRunning!
            return

        if @GetTeamAliveCount( TEAM_GUARD ) == 0 or @GetTeamAliveCount( TEAM_PRISONER ) == 0
            Jailbreak.SetRoundState( ROUND_ENDED )

GM.TeamPlayerDisconnected = ( ply, teamID ) =>
    unless ply\Alive!
        return

    timer.Create "Jailbreak::TeamPlayerDisconnected", 0.5, 1, ->
        timer.Remove "Jailbreak::TeamPlayerDisconnected"
        unless Jailbreak.IsRoundRunning!
            return

        if @GetTeamAliveCount( TEAM_GUARD ) == 0 or @GetTeamAliveCount( TEAM_PRISONER ) == 0
            Jailbreak.SetRoundState( ROUND_ENDED )

    ply\CreateRagdoll!

GM.TeamPlayerSpawn = ( ply ) =>
    unless Jailbreak.IsWaitingPlayers!
        return

    if @GetTeamAliveCount( TEAM_GUARD ) > 0 and @GetTeamAliveCount( TEAM_PRISONER ) > 0
        Jailbreak.SetRoundState( ROUND_PREPARING )
        Jailbreak.SetRoundTime( @PrepareTime\GetInt! )

concommand.Add "changeteam", ( _, args ) =>
    hook_Run( "PlayerRequestTeam", @, tonumber( args[ 1 ] ) )

concommand.Add "jb_warden_request", ( _, args ) =>
    unless IsValid( @ ) and not @IsWarden!
        return

    unless Jailbreak.IsRoundRunning! and @Alive! and @IsGuard!
        @SendNotify( "#jb.error.warden-failure", NOTIFY_ERROR, 10 )
        return

    if Jailbreak.HasWarden!
        @SendNotify( "#jb.error.warden-exists", NOTIFY_ERROR, 10 )
        return

    @SetWarden( true )

concommand.Add "jb_toggle_electro_collars", ( _, args ) =>
    unless IsValid( @ )
        return

    unless Jailbreak.IsRoundRunning! and @Alive! and @IsWarden!
        @SendNotify( "#jb.error.cant-do-that", NOTIFY_ERROR, 10 )
        return

    requested = args[ 1 ]
    if requested ~= nil and #requested ~= 0
        Jailbreak.SetElectroCollars( requested == "1" )
    else
        Jailbreak.SetElectroCollars( not Jailbreak.IsElectroCollarsActive! )

do

    :lower, :find = string

    do

        replaceFilter = ( modelName, chance ) ->
            unless isnumber( chance )
                chance = nil

            if isstring( modelName )
                modelName = lower( modelName )
            elseif istable( modelName )
                tbl = {}
                for str in *modelName
                    tbl[ lower( str ) ] = true

                modelName = tbl
            else
                return =>
                    return not chance or random( 1, 100 ) <= chance

            return =>
                modelPath = @GetModel!
                unless modelPath
                    return false

                modelPath = lower( modelPath )
                unless IsValidModel( modelPath )
                    return false

                if istable( modelName ) and not modelName[ modelPath ]
                    return false

                unless find( modelPath, modelName, 1, false )
                    return false

                return not chance or random( 1, 100 ) <= chance

        Addons = {

            -- https://steamcommunity.com/sharedfiles/filedetails/?id=293904092
            ["293904092"]: ->
                EntityReplacer "^prop_physics.*", "sent_soccerball", replaceFilter( "models/props_phx/misc/soccerball%.mdl" )

            -- https://steamcommunity.com/sharedfiles/filedetails/?id=931376012
            ["931376012"]: ->
                EntityReplacer "^prop_physics.*", "sent_jetpack", replaceFilter( "models/props_junk/trafficcone001a%.mdl", 10 ), =>
                    @SetSlotName( @GetClass! )

            -- https://steamcommunity.com/sharedfiles/filedetails/?id=931448005
            ["931448005"]: ->
                EntityReplacer "^prop_physics.*", "sent_grapplehook_bpack", replaceFilter( "models/props_junk/plasticcrate%w+%.mdl", 20 ), =>
                    @SetSlotName( @GetClass! )

                    timer_Simple 0, ->
                        if @IsValid!
                            @SetKey( KEY_B )

        }

        do
            fileName = "jailbreak/entity-replacer.json"
            if file.Exists( fileName, "DATA" )
                json = file.Read( fileName, "DATA" )
                if isstring( json )
                    data = util.JSONToTable( json )
                    if istable( data )
                        for key, value in pairs( data )
                            Addons[ key ] = ->
                                return EntityReplacer value[ 1 ], value[ 2 ], replaceFilter( value[ 3 ], value[ 4 ] )

            else
                file.Write( fileName, "[]" )

        global PropReplaceFilter = replaceFilter
        Jailbreak.Addons = Addons

        timer_Simple 0, ->
            for addon in *engine.GetAddons!
                unless addon.downloaded and addon.mounted
                    continue

                wsid = addon.wsid
                func = Addons[ wsid ]
                unless func
                    continue

                Jailbreak[ wsid ] = func( addon )

    do

        ChatCommands = Jailbreak.ChatCommands
        unless istable( ChatCommands )
            ChatCommands = {}
            Jailbreak.ChatCommands = ChatCommands

        Jailbreak.SetChatCommand = ( str, func ) ->
            ChatCommands[ lower( str ) ] = func

        GM.PlayerSay = ( ply, text, isTeam ) =>
            if Classic
                return text

            if string.StartsWith( text, "/" )
                startPos, endPos, command = find( text, "^/([^%s]+)" )
                if command
                    func = ChatCommands[ lower( command ) ]
                    if func
                        return func( ply, string.sub( text, min( endPos + 2, #text ) ) ) or ""
                return ""

            if isTeam
                ply\Say( text, false )
                return ""
            elseif ply\UsingSecurityRadio!
                players = {}
                for pl in *player.GetHumans!
                    if pl\HasSecurityRadio!
                        players[] = pl

                Jailbreak.SendChatText( players, "[#jb.walkie-talkie] ", ply\GetPlayerColor!\ToColor!, ply\Nick!, color_white, " #jb.chat.says: \"" .. text .. "\"" )
                Jailbreak.Whisper( ply, text )
                return ""

            unless Jailbreak.GameInProgress!
                Jailbreak.OOC( ply, text )
                return ""

            return text

Jailbreak.SetChatCommand "warden", =>
    @ConCommand( "jb_warden_request" )

do

    color_white = color_white

    do

        Distance = CreateConVar( "chat_whisper_distance", "32", ConVarFlags, "", 16, 65536 )

        Whisper = ( text ) =>
            Jailbreak.SendChatText( @GetNearPlayer( Distance\GetInt!, false ), @GetPlayerColor!\ToColor!, @Nick!, color_white, " #jb.chat.whispers, \"" .. text .. "\"" )
        Jailbreak.Whisper = Whisper

        Jailbreak.SetChatCommand "whisper", Whisper
        Jailbreak.SetChatCommand "w", Whisper

    do

        Distance = CreateConVar( "chat_looc_distance", "128", ConVarFlags, "", 48, 65536 )
        blue = Color( 0, 190, 255 )
        :GetColor = team

        Jailbreak.SetChatCommand "looc", ( text ) =>
            Jailbreak.SendChatText( @GetNearPlayer( Distance\GetInt!, false ), blue, "[#jb.chat.looc] ", GetColor( @Team! ), @Nick!, color_white, ": " .. text )

        Me = ( text ) =>
            Jailbreak.SendChatText( @GetNearPlayer( Distance\GetInt!, false ), @GetPlayerColor!\ToColor!, @Nick!, color_white, " " .. text )

        Jailbreak.SetChatCommand "me", Me

        do

            CoinFlip = ( _, text ) =>
                Me( @, "#jb.chat.coin-flip #jb.chat.coin-flip." .. math.random( 0, 1 ) )

            Jailbreak.SetChatCommand "coin", CoinFlip
            Jailbreak.SetChatCommand "flip", CoinFlip

        Jailbreak.SetChatCommand "roll", ( text ) =>
            int1, int2 = string.match( text, "(-?%d+)%s+(-?%d+)" )
            unless int1 and int2
                @SendNotify( "#jb.chat.fail", NOTIFY_ERROR, 5 )
                return

            Me( @, "#jb.chat.rolled " .. math.random( tonumber( int1 ) or 0, tonumber( int2 ) or 0 ) )

    do

        Enabled = CreateConVar( "chat_ooc", "0", ConVarFlags, "Allows non-game global chat to be used by everyone.", 0, 1 )
        turquoise = Color( 75, 200, 200 )

        OOC = ( text ) =>
            Jailbreak.SendChatText( false, turquoise, "[#jb.chat.ooc] ", @GetPlayerColor!\ToColor!, @Nick!, color_white, ": " .. text )
        Jailbreak.OOC = OOC

        Jailbreak.SetChatCommand "ooc", ( text ) =>
            if not Enabled\GetBool! and not @IsAdmin! and Jailbreak.GameInProgress!
                @SendNotify( "#jb.chat.fail", NOTIFY_ERROR, 5 )
                return

            OOC( @, text )
