RunConsoleCommand = RunConsoleCommand
Jailbreak = Jailbreak
hook_Run = hook.Run
:format = string
NULL = NULL
GM = GM

GM.InitPostEntity = =>
    Jailbreak.PlayingTaunt = false

    do
        ply = LocalPlayer!
        Jailbreak.Player = ply
        Jailbreak.PlayerIndex = ply\EntIndex!

    timer.Simple 0.5, ->
        RunConsoleCommand( "r_flushlod" )
        RunConsoleCommand( "jb_showteam" )
        RunConsoleCommand( "dsp_room", "1" )

GM.PostCleanupMap = =>
    RunConsoleCommand( "r_cleardecals" )

GM.OnSpawnMenuOpen = =>
    RunConsoleCommand( "lastinv" )

GM.PostProcessPermitted = =>
    return false

do

    IsFirstTimePredicted = IsFirstTimePredicted
    :LookupKeyBinding, :IsKeyDown = input

    GM.PlayerButtonUp = ( ply, keyCode ) =>
        bind = LookupKeyBinding( keyCode )
        if bind and #bind ~= 0
            return

        unless IsFirstTimePredicted!
            return

        if keyCode == 17
            RunConsoleCommand( "drop" )
        elseif keyCode == 109
            RunConsoleCommand( "marker" )
        elseif keyCode == 58 or keyCode == 23
            RunConsoleCommand( "jb_showteam" )

    GM.PlayerButtonDown = ( ply, keyCode ) =>
        if keyCode == 107 and IsKeyDown( 81 )
            RunConsoleCommand( "marker" )

do

    :ReadString, :ReadUInt, :ReadTable, :ReadEntity, :ReadBool = net
    :AddLegacy = notification
    :Translate = Jailbreak
    Entity = Entity
    unpack = unpack
    :random = math

    Networking = {
        [ 0 ]: ->
            gameName = ReadString!
            SoundHandler( gameName )
            Jailbreak.GameName = gameName
        [ 1 ]: ->
            hook_Run( "HUDItemPickedUp", ReadString! )
        [ 2 ]: ->
            AddLegacy( format( Translate( ReadString! ), unpack( ReadTable( true ) ) ), ReadUInt( 3 ), ReadUInt( 16 ) )
        [ 3 ]: ->
            hook_Run( "OnChatText", ReadBool! and Entity( ReadUInt( 8 ) ) or NULL, ReadUInt( 4 ), ReadTable( true ) )
        [ 4 ]: ->
            ply = Jailbreak.Player
            unless ply\IsValid!
                return

            ply\EmitSound( ReadString!, 75, random( 90, 110 ), 1, CHAN_STATIC, 0, 1 )
        [ 5 ]: ->
            entity = ReadEntity!
            if entity and entity\IsValid and entity\IsPlayer!
                entity\AnimRestartGesture( ReadUInt( 3 ), ReadUInt( 11 ), ReadBool! )
        [ 6 ]: ->
            RunConsoleCommand( "jb_security_radio", "0" )
            RunConsoleCommand( "jb_megaphone", "0" )
    }

    net.Receive "Jailbreak::Networking", ( len ) ->
        func = Networking[ ReadUInt( 3 ) ]
        unless func
            return

        func( len )

do

    CHAT_SERVERMESSAGE = CHAT_SERVERMESSAGE

    GM.ChatText = ( index, nickname, text, messageType ) =>
        if messageType == "servermsg"
            hook_Run( "OnChatText", NULL, CHAT_SERVERMESSAGE, { text } )

        return true

GM.OnPlayerChat = =>
    return true
