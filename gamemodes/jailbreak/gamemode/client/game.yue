RunConsoleCommand = RunConsoleCommand
Jailbreak = Jailbreak
:format = string
GM = GM

GM.InitPostEntity = =>
    Jailbreak.PlayingTaunt = false

    ply = LocalPlayer!
    Jailbreak.Player = ply
    Jailbreak.PlayerIndex = ply\EntIndex!

    timer.Simple 0.25, ->
        RunConsoleCommand( "dsp_player", "1" )
        RunConsoleCommand( "dsp_room", "1" )

    timer.Simple 1, ->
        RunConsoleCommand( "r_flushlod" )

    return

GM.PostCleanupMap = =>
    RunConsoleCommand( "r_cleardecals" )

GM.OnSpawnMenuOpen = =>
    RunConsoleCommand( "lastinv" )

GM.PostProcessPermitted = =>
    return false

do

    IsFirstTimePredicted = IsFirstTimePredicted
    :LookupKeyBinding, :IsKeyDown = input

    GM.PlayerButtonUp = ( ply, keyCode ) =>
        unless IsFirstTimePredicted!
            return

        bind = LookupKeyBinding( keyCode )
        if keyCode == 17 and ( not bind or bind == "drop" or #bind == 0 )
            RunConsoleCommand( "drop" )
        elseif keyCode == 109 and ( not bind or bind == "marker" or #bind == 0 )
            RunConsoleCommand( "marker" )
        elseif ( keyCode == 58 or keyCode == 23 ) and ( not bind or bind == "jb_showteam" or #bind == 0 )
            RunConsoleCommand( "jb_showteam" )
        elseif keyCode == 18 and ( not bind or #bind == 0 )
            RunConsoleCommand( "pe_drop", "movement" )

    GM.PlayerButtonDown = ( ply, keyCode ) =>
        if ( keyCode == 107 or keyCode == 108 ) and IsKeyDown( 81 ) and IsFirstTimePredicted!
            RunConsoleCommand( "marker" )

do

    :ReadString, :ReadUInt, :ReadTable, :ReadEntity, :ReadBool = net
    CHAN_STATIC = CHAN_STATIC
    :AddLegacy = notification
    :Translate = Jailbreak
    unpack = unpack
    :random = math
    :Run = hook

    Networking = {
        [ 0 ]: ->
            gameName = ReadString!
            SoundHandler( gameName )
            Jailbreak.GameName = gameName

        [ 1 ]: ->
            Run( "HUDItemPickedUp", ReadString! )

        [ 2 ]: ->
            AddLegacy( format( Translate( ReadString! ), unpack( ReadTable( true ) ) ), ReadUInt( 3 ), ReadUInt( 16 ) )

        [ 3 ]: ->
            ply = Jailbreak.Player
            if ply\IsValid!
                ply\EmitSound( ReadString!, 75, random( 90, 110 ), 1, CHAN_STATIC, 0, 1 )

        [ 4 ]: ->
            entity = ReadEntity!
            if entity and entity\IsValid and entity\IsPlayer!
                entity\AnimRestartGesture( ReadUInt( 3 ), ReadUInt( 11 ), ReadBool! )

        [ 5 ]: ->
            RunConsoleCommand( "jb_security_radio", "0" )
            RunConsoleCommand( "jb_megaphone", "0" )

    }

    net.Receive "Jailbreak::Networking", ->
        func = Networking[ ReadUInt( 4 ) ]
        if func ~= nil
            func!

concommand.Add "jb_credits", ->
    MsgC( unpack( Jailbreak.Credits ) )
