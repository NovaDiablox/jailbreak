RunConsoleCommand = RunConsoleCommand
Jailbreak = Jailbreak
:format = string
GM = GM

Jailbreak.VoiceChatState = Jailbreak.VoiceChatState or false
Jailbreak.PlayingTaunt = Jailbreak.PlayingTaunt or false
Jailbreak.TauntFraction = Jailbreak.TauntFraction or 0
Jailbreak.ViewEntity = Jailbreak.ViewEntity or NULL
Jailbreak.Player = Jailbreak.Player or NULL

hook.Add "RenderScene", "Jailbreak::PlayerInitialized", ->
    hook.Remove "RenderScene", "Jailbreak::PlayerInitialized"
    unless Jailbreak.Player\IsValid!
        ply = LocalPlayer!
        Jailbreak.Player = ply
        Jailbreak.PlayerIndex = ply\EntIndex!
        hook.Run( "PlayerInitialized", ply )
        return true

GM.PlayerInitialized = =>
    RunConsoleCommand( "dsp_player", "1" )
    RunConsoleCommand( "dsp_room", "1" )

GM.InitPostEntity = =>
    RunConsoleCommand( "r_flushlod" )

GM.PostCleanupMap = =>
    RunConsoleCommand( "r_cleardecals" )

GM.OnSpawnMenuOpen = =>
    RunConsoleCommand( "lastinv" )

GM.PostProcessPermitted = =>
    return false

do

    Jailbreak.DrawHUD = GetConVar( "cl_drawhud" )\GetBool!

    cvars.AddChangeCallback( "cl_drawhud", ( _, __, value ) ->
        Jailbreak.DrawHUD = value ~= "0"

        panel = Jailbreak.HUD
        if panel and panel\IsValid!
            panel\SetVisible( Jailbreak.DrawHUD )

    "Jailbreak::DrawHUD" )

do

    :PlayerModel = Jailbreak

    do

        requestedName = PlayerModel\GetString!

        modelName = Jailbreak.FormatPlayerModelName( requestedName )
        Jailbreak.SelectedPlayerModel = modelName

        if modelName ~= requestedName
            PlayerModel\SetString( modelName )
            hook.Run( "PlayerModelChanged", modelName )

    cvars.AddChangeCallback( PlayerModel\GetName!, ( _, __, requestedName ) ->
        Jailbreak.PlayerBodyGroups\SetInt( 0 )
        Jailbreak.PlayerSkin\SetInt( 0 )

        modelName = Jailbreak.FormatPlayerModelName( requestedName )
        Jailbreak.SelectedPlayerModel = modelName

        if modelName ~= requestedName
            PlayerModel\SetString( modelName )

        hook.Run( "PlayerModelChanged", modelName )
        return
    "Jailbreak::PlayerModel" )

do

    IsFirstTimePredicted = IsFirstTimePredicted
    :LookupKeyBinding, :IsKeyDown = input

    GM.PlayerButtonUp = ( ply, keyCode ) =>
        unless IsFirstTimePredicted!
            return

        bind = LookupKeyBinding( keyCode )
        if keyCode == 17 and ( not bind or bind == "drop" or #bind == 0 )
            RunConsoleCommand( "drop" )
        elseif keyCode == 109 and ( not bind or bind == "marker" or #bind == 0 )
            RunConsoleCommand( "marker" )
        elseif ( keyCode == 58 or keyCode == 23 ) and ( not bind or bind == "jb_showteam" or #bind == 0 )
            RunConsoleCommand( "jb_showteam" )
        elseif keyCode == 18 and ( not bind or #bind == 0 )
            RunConsoleCommand( "pe_drop", "movement" )

    GM.PlayerButtonDown = ( ply, keyCode ) =>
        if ( keyCode == 107 or keyCode == 108 ) and IsKeyDown( 81 ) and IsFirstTimePredicted!
            RunConsoleCommand( "marker" )

do

    :ReadString, :ReadUInt, :ReadTable, :ReadEntity, :ReadBool = net
    CHAN_STATIC = CHAN_STATIC
    :AddLegacy = notification
    :Translate = Jailbreak
    unpack = unpack
    :random = math
    :Run = hook

    net.Receive "Jailbreak::Networking", ->
        switch ReadUInt( 4 )
            when 0
                gameName = ReadString!
                SoundHandler( gameName )
                Jailbreak.m_sGameName = gameName

            when 1
                Run( "PickupNotifyReceived", ReadString!, ReadUInt( 6 ), ReadUInt( 16 ) )

            when 2
                AddLegacy( format( Translate( ReadString! ), unpack( ReadTable( true ) ) ), ReadUInt( 3 ), ReadUInt( 16 ) )

            when 3
                ply = Jailbreak.Player
                if ply\IsValid!
                    ply\EmitSound( ReadString!, 75, random( 90, 110 ), 1, CHAN_STATIC, 0, 1 )

            when 4
                entity = ReadEntity!
                if entity and entity\IsValid and entity\IsPlayer!
                    entity\AnimRestartGesture( ReadUInt( 3 ), ReadUInt( 11 ), ReadBool! )

            when 5
                RunConsoleCommand( "jb_megaphone", GetConVar( "jb_megaphone" )\GetDefault! )
                RunConsoleCommand( "jb_security_radio", GetConVar( "jb_security_radio" )\GetDefault! )

concommand.Add "jb_credits", ->
    MsgC( unpack( Jailbreak.Credits ) )
