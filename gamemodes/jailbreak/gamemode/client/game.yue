RunConsoleCommand = RunConsoleCommand
Jailbreak = Jailbreak
hook_Run = hook.Run
:format = string
GM = GM

GM.InitPostEntity = =>
    RunConsoleCommand( "dsp_room", "1" )
    Jailbreak.PlayingTaunt = false

    do
        ply = LocalPlayer!
        Jailbreak.Player = ply
        Jailbreak.PlayerIndex = ply\EntIndex!

    timer.Simple 0.5, ->
        RunConsoleCommand( "r_flushlod" )
        RunConsoleCommand( "jb_showteam" )

GM.PostCleanupMap = =>
    RunConsoleCommand( "r_cleardecals" )

GM.OnSpawnMenuOpen = =>
    RunConsoleCommand( "lastinv" )

GM.PostProcessPermitted = =>
    return false

do

    :LookupKeyBinding, :IsKeyDown = input

    GM.PlayerButtonUp = ( ply, keyCode ) =>
        bind = LookupKeyBinding( keyCode )
        if bind and #bind ~= 0
            return

        if keyCode == 17
            RunConsoleCommand( "drop" )
        elseif keyCode == 109
            RunConsoleCommand( "marker" )
        elseif keyCode == 58 or keyCode == 23
            RunConsoleCommand( "jb_showteam" )

    GM.PlayerButtonDown = ( ply, keyCode ) =>
        if keyCode == 107 and IsKeyDown( 81 )
            RunConsoleCommand( "marker" )

do

    :ReadString, :ReadUInt, :ReadTable, :ReadEntity, :ReadBool = net
    :ChatText, :Translate = Jailbreak
    :AddLegacy = notification
    :PlaySound = surface
    isstring = isstring
    unpack = unpack

    Networking = {
        [ 0 ]: ->
            gameName = ReadString!
            SoundHandler( gameName )
            Jailbreak.GameName = gameName
        [ 1 ]: ->
            hook_Run( "HUDItemPickedUp", ReadString! )
        [ 2 ]: ->
            AddLegacy( format( Translate( ReadString! ), unpack( ReadTable( true ) ) ), ReadUInt( 3 ), ReadUInt( 16 ) )
        [ 3 ]: ->
            data = ReadTable( true )
            for index = 1, #data
                value = data[ index ]
                if isstring( value )
                    data[ index ] = Translate( value )

            ChatText( unpack( data ) )
        [ 4 ]: ->
            PlaySound( ReadString! )
        [ 5 ]: ->
            entity = ReadEntity!
            if entity and entity\IsValid and entity\IsPlayer!
                entity\AnimRestartGesture( ReadUInt( 3 ), ReadUInt( 11 ), ReadBool! )
        [ 6 ]: ->
            RunConsoleCommand( "jb_security_radio", "0" )
            RunConsoleCommand( "jb_megaphone", "0" )
    }

    net.Receive "Jailbreak::Networking", ( len ) ->
        func = Networking[ ReadUInt( 3 ) ]
        unless func
            return

        func( len )
