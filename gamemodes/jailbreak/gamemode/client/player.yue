Jailbreak = Jailbreak
:Clamp = math
drive = drive
:Run = hook
GM = GM

unless Jailbreak.ViewEntity
    Jailbreak.ViewEntity = NULL

unless Jailbreak.Player
    Jailbreak.Player = NULL

GM.ShouldDrawLocalPlayer = ( ply ) =>
    if ply ~= Jailbreak.ViewEntity
        if Jailbreak.PlayingTaunt
            Jailbreak.PlayingTaunt = false

        return true

    if ply\IsPlayingTaunt!
        Jailbreak.PlayingTaunt = true
        return true

    Jailbreak.PlayingTaunt = false

    if Jailbreak.TauntFraction > 0
        return true

    return false

do

    LerpVector = LerpVector
    LerpAngle = LerpAngle
    :TraceHull = util
    :CalcView = drive

    tauntTrace = {
        mask: MASK_SHOT
        mins: Vector( -8, -8, -8 )
        maxs: Vector( 8, 8, 8 )
    }

    view = Jailbreak.PlayerView
    unless istable( view )
        view = {}
        Jailbreak.PlayerView = view

    GM.CalcView = ( ply, origin, angles, fov, znear, zfar ) =>
        Jailbreak.Player = ply

        view.origin = origin
        view.angles = angles
        view.fov = fov
        view.znear = znear
        view.zfar = zfar
        view.drawviewer = false

        if CalcView( ply, view )
            return view

        entity = nil
        if ply\Alive!
            if ply\GetNW2Bool( "lost-consciousness" )
                entity = ply\GetRagdollEntity!
                unless entity\IsValid!
                    return view

                attachmentID = entity\LookupAttachment( "eyes" )
                if attachmentID >= 0
                    attachment = entity\GetAttachment( attachmentID )
                    if attachment
                        view.origin = attachment.Pos
                        view.angles = attachment.Ang

                view.drawviewer = false
                return view

        else
            entity = ply\GetObserverTarget!

        unless entity and entity\IsValid!
            entity = ply\GetViewEntity!

        unless entity and entity\IsValid!
            entity = ply

        Jailbreak.ViewEntity = entity

        if ply ~= entity
            return view

        unless ply\Alive!
            return view

        if entity\Health! < 1
            attachmentID = entity\LookupAttachment( "eyes" )
            if attachmentID >= 0
                attachment = entity\GetAttachment( attachmentID )
                if attachment
                    view.origin = attachment.Pos
                    view.angles = attachment.Ang

            view.drawviewer = true

            return view

        if Jailbreak.PlayingTaunt or Jailbreak.TauntFraction > 0
            :TauntViewAngles = Jailbreak
            unless TauntViewAngles
                TauntViewAngles = angles
                Jailbreak.TauntViewAngles = TauntViewAngles

            :TauntDistance = Jailbreak
            unless TauntDistance
                TauntDistance = 128
                Jailbreak.TauntDistance = TauntDistance

            bone = ply\LookupBone( "ValveBiped.Bip01_Head1" )
            if bone and bone >= 0
                origin = ply\GetBonePosition( bone )

            targetOrigin = origin - TauntViewAngles\Forward! * TauntDistance

            tauntTrace.start = origin
            tauntTrace.endpos = targetOrigin
            tauntTrace.filter = entity

            traceResult = TraceHull( tauntTrace )
            targetOrigin = traceResult.HitPos + traceResult.HitNormal

            :TauntPlayerAngles = Jailbreak
            unless TauntPlayerAngles
                TauntPlayerAngles = Angle( angles )
                Jailbreak.TauntPlayerAngles = TauntPlayerAngles

            :TauntFraction = Jailbreak
            if Jailbreak.PlayingTaunt
                if TauntFraction < 1
                    TauntFraction += FrameTime! * 4
                    Jailbreak.TauntFraction = TauntFraction

                    view.origin = LerpVector( TauntFraction, origin, targetOrigin )
                    view.angles = LerpAngle( TauntFraction, TauntPlayerAngles, TauntViewAngles )
                    return view

            else

                if TauntFraction > 0
                    TauntFraction -= FrameTime! * 4
                    Jailbreak.TauntFraction = TauntFraction

                    view.origin = LerpVector( TauntFraction, origin, targetOrigin )
                    view.angles = LerpAngle( TauntFraction, TauntPlayerAngles, TauntViewAngles )
                    return view

            view.origin = targetOrigin
            view.angles = TauntViewAngles
            return view

        if Jailbreak.TauntViewAngles
            Jailbreak.TauntViewAngles = nil

        if Jailbreak.TauntDistance
            Jailbreak.TauntDistance = nil

        if Jailbreak.TauntPlayerAngles
            Jailbreak.TauntPlayerAngles = nil

        if ply\InVehicle!
            return Run( "CalcVehicleView", ply\GetVehicle!, ply, view )

        weapon = ply\GetActiveWeapon!
        if weapon and weapon\IsValid!
            func = weapon.CalcView
            if func
                origin, angles, fov = func( weapon, ply, Vector( view.origin ), Angle( view.angles ), view.fov )
                view.origin, view.angles, view.fov = origin or view.origin, angles or view.angles, fov or view.fov

        return view

do

    :CreateMove = drive

    GM.CreateMove = ( cmd ) =>
        if CreateMove( cmd )
            return true

        if Jailbreak.PlayingTaunt
            :TauntViewAngles = Jailbreak
            if TauntViewAngles
                TauntViewAngles[ 1 ] += cmd\GetMouseY! * 0.01
                TauntViewAngles[ 2 ] -= cmd\GetMouseX! * 0.01

            :TauntDistance = Jailbreak
            if TauntDistance
                Jailbreak.TauntDistance = Clamp( TauntDistance - cmd\GetMouseWheel! * ( TauntDistance * 0.25 ), 16, 1024 )

            :TauntPlayerAngles = Jailbreak
            if TauntPlayerAngles
                cmd\SetViewAngles( TauntPlayerAngles )

            cmd\ClearMovement!
            cmd\ClearButtons!
            return true

GM.PreDrawViewModel = ( vm, ply, weapon ) =>
    func = weapon.PreDrawViewModel
    if func == nil
        return false

    return func( weapon, vm, weapon, ply )

do

    MATERIAL_CULLMODE_CCW = MATERIAL_CULLMODE_CCW
    MATERIAL_CULLMODE_CW = MATERIAL_CULLMODE_CW
    :DrawModel = FindMetaTable( "Entity" )
    :CullMode = render

    GM.PostDrawViewModel = ( vm, ply, weapon ) =>
        if weapon.UseHands or not weapon\IsScripted!
            hands = ply\GetHands!
            if hands and hands\IsValid! and hands\GetParent!\IsValid!
                unless Run( "PreDrawPlayerHands", hands, vm, ply, weapon )
                    if weapon.ViewModelFlip
                        CullMode( MATERIAL_CULLMODE_CW )

                    DrawModel( hands )
                    CullMode( MATERIAL_CULLMODE_CCW )

                Run( "PostDrawPlayerHands", hands, vm, ply, weapon )

        func = weapon.PostDrawViewModel
        if func == nil
            return false

        return func( weapon, vm, weapon, ply )

do

    :HandsTransparency = Jailbreak

    handsAlpha = 1 - HandsTransparency\GetFloat!
    tonumber = tonumber
    :SetBlend = render

    cvars.AddChangeCallback( HandsTransparency\GetName!, ( _, __, value ) ->
        handsAlpha = 1 - ( tonumber( value ) or 0 )
    "Jailbreak::HandsTransparency" )

    GM.PreDrawPlayerHands = =>
        SetBlend( handsAlpha )

    GM.PostDrawPlayerHands = =>
        SetBlend( 1 )
