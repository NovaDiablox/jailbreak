Jailbreak = Jailbreak
hook_Run = hook.Run
GM = GM

:Colors, :VMin = Jailbreak
:white, :black, :dark_grey = Colors

:SetFont, :GetTextSize, :DrawText, :SetTextColor, :SetTextPos = surface
:sub, :format = string
:GetPhrase = language
EyePos = EyePos

ROUND_WAITING_PLAYERS = ROUND_WAITING_PLAYERS
ROUND_RUNNING = ROUND_RUNNING
ROUND_ENDED = ROUND_ENDED

:GetName, :GetColor = team

:SourceHUD = Jailbreak
for className in *SourceHUD
    SourceHUD[ className ] = true

GM.HUDShouldDraw = ( name ) =>
    if SourceHUD[ name ] or ( Jailbreak.PlayingTaunt and name == "CHudWeaponSelection" )
        return false

    return true

do

    :GetRoundState, :GetRemainingTime, :GetWinningTeam, :Teams = Jailbreak

    Jailbreak.Font( "Jailbreak::Winners", "Roboto Mono Bold Italic", 4 )
    Jailbreak.Font( "Jailbreak::RoundState", "Roboto Mono Medium", 4 )

    GM.HUDPaint = =>
        hook_Run( "HUDDrawIcons", Jailbreak.Player )

        if @TargetID\GetBool!
            hook_Run( "HUDDrawTargetID" )

        hook_Run( "HUDDrawPickupHistory" )

        if @DeathNotice\GetBool!
            hook_Run( "DrawDeathNotice", 0.85, 0.04 )

        state = GetRoundState!
        if state == ROUND_RUNNING
            return

        text = GetPhrase( "jb.round." .. state )
        if state ~= ROUND_WAITING_PLAYERS
            remainingTime = GetRemainingTime!
            if remainingTime == 0
                return

            text = format( text, remainingTime )

        SetFont( "Jailbreak::RoundState" )

        screenCenterX = Jailbreak.ScreenCenterX
        textWidth, textHeight = GetTextSize( text )
        x, y = screenCenterX - textWidth / 2, VMin( 1 )

        SetTextPos( x - 1, y - 1 )
        SetTextColor( black.r, black.g, black.b, 50 )
        DrawText( text )

        SetTextPos( x + 3, y + 3 )
        SetTextColor( black.r, black.g, black.b, 120 )
        DrawText( text )

        SetTextColor( white )
        SetTextPos( x, y )
        DrawText( text )

        if state == ROUND_ENDED
            teamID, color = GetWinningTeam!, nil
            if Teams[ teamID ]
                text = format( GetPhrase( "#jb.victory" ), GetPhrase( GetName( teamID ) ) )
                color = GetColor( teamID )
            else
                text = GetPhrase( "#jb.draw" )
                color = Colors.light_grey

            text = "[" .. text .. "]"

            textWidth2, textHeight2 = GetTextSize( text )
            x, y = screenCenterX - textWidth2 / 2, y + ( textHeight + textHeight2 ) / 2

            SetTextPos( x - 1, y - 1 )
            SetTextColor( black.r, black.g, black.b, 50 )
            DrawText( text )

            SetTextPos( x + 3, y + 3 )
            SetTextColor( black.r, black.g, black.b, 120 )
            DrawText( text )

            SetTextColor( color )
            SetTextPos( x, y )
            DrawText( text )

do

    :Translate, :VoiceChatMinDistance = Jailbreak
    :CursorVisible = vgui
    :GetCursorPos = input
    EyeAngles = EyeAngles
    MASK_SHOT = MASK_SHOT
    :TraceLine = util
    :Clamp = math
    :red = Colors
    Lerp = Lerp

    trace = {
        mask: MASK_SHOT
    }

    Jailbreak.Font( "Jailbreak::TargetID", "Roboto Mono Bold", 2.6 )
    Jailbreak.Font( "Jailbreak::TargetIDSmall", "Roboto Mono SemiBold Italic", 2.6 )

    GM.HUDDrawTargetID = =>
        ply = Jailbreak.Player
        unless ply\IsValid!
            return

        start = EyePos!
        trace.start = start

        endpos, filter = nil, nil
        if ply\GetObserverMode! == OBS_MODE_IN_EYE
            target = Jailbreak.ViewEntity
            if target\IsValid!
                filter = target

                if target\IsPlayer! and target\Alive!
                    endpos = start + target\GetAimVector! * VoiceChatMinDistance\GetInt!

        elseif ply\Alive! and not Jailbreak.PlayingTaunt
            endpos = start + ply\GetAimVector! * VoiceChatMinDistance\GetInt!
            filter = ply

        unless endpos
            endpos = start + EyeAngles!\Forward! * VoiceChatMinDistance\GetInt!

        trace.endpos, trace.filter = endpos, filter

        traceResult = TraceLine( trace )
        unless traceResult.Hit
            return

        if traceResult.HitWorld
            return

        entity = traceResult.Entity
        unless entity\IsValid!
            return

        isPlayerRagdoll = entity\IsPlayerRagdoll!
        isPlayer = entity\IsPlayer!
        color = white

        text = nil
        if isPlayer
            unless entity\Alive!
                return

            text = entity\Nick!
            color = entity\GetModelColor!
            frac = Clamp( entity\Health! / entity\GetMaxHealth!, 0, 1 )

            if ( color.r + color.g + color.b ) < 125
                color.r = Lerp( frac, red.r, color.r )
                color.g = Lerp( frac, red.g, color.g )
                color.b = Lerp( frac, red.b, color.b )
            else
                color.r = Lerp( frac, 0, color.r )
                color.g = Lerp( frac, 0, color.g )
                color.b = Lerp( frac, 0, color.b )

        elseif isPlayerRagdoll
            text = entity\GetNW2String( "owner-nickname", "unknown" )
            color = entity\GetModelColor!

        elseif entity\IsWeapon!
            printName = nil
            if entity\IsScripted!
                printName = entity.PrintName

            if printName
                printName = "#" .. printName
            else
                printName = entity\GetPrintName!

            if printName == "Scripted Weapon"
                printName = "#" .. entity\GetClass!

            text = Translate( printName )

        else

            className = entity\GetClass!
            if className == "class C_BaseEntity"
                className = "func_button"

            text = Translate( "#jb." .. className )

        mouseX, mouseY = GetCursorPos!
        if mouseX == 0 and mouseY == 0 or not CursorVisible!
            mouseX, mouseY = Jailbreak.ScreenCenterX, Jailbreak.ScreenCenterY

        offset = VMin( 2 )

        SetFont( "Jailbreak::TargetID" )
        textWidth, textHeight = GetTextSize( text )

        x = mouseX - textWidth / 2
        y = mouseY + offset

        SetTextPos( x - 1, y - 1 )
        SetTextColor( black.r, black.g, black.b, 50 )
        DrawText( text )

        SetTextPos( x + 3, y + 3 )
        SetTextColor( black.r, black.g, black.b, 120 )
        DrawText( text )

        SetTextColor( color )
        SetTextPos( x, y )
        DrawText( text )

        color = nil
        if isPlayer
            teamID = entity\Team!
            text = GetName( teamID )
            color = GetColor( teamID )
        elseif isPlayerRagdoll
            text = "#jb.player.corpse"
            color = Colors.light_grey
        else
            return

        SetFont( "Jailbreak::TargetIDSmall" )
        textWidth2, textHeight2 = GetTextSize( text )
        x, y = mouseX - textWidth2 / 2, y + ( textHeight + textHeight2 ) / 2

        SetTextPos( x - 1, y - 1 )
        SetTextColor( black.r, black.g, black.b, 50 )
        DrawText( text )

        SetTextPos( x + 3, y + 3 )
        SetTextColor( black.r, black.g, black.b, 120 )
        DrawText( text )

        SetTextColor( color )
        SetTextPos( x, y )
        DrawText( text )

do

    SpeakingIcon = Material( "icon16/sound.png", "mips" )
    FrameTime = FrameTime
    CurTime = CurTime
    :sin = math

    do

        :DrawTexturedRectRotated, :SetMaterial, :SetDrawColor, :SetAlphaMultiplier = surface

        Jailbreak.Font( "Jailbreak::VoiceChatIcon", "Roboto Mono Medium", 3 )
        alpha = 0

        hook.Add "HUDDrawIcons", "Jailbreak::VoiceChatIcon", ( ply ) ->
            if ply\IsSpeaking!
                if alpha < 1
                    alpha = math.min( 1, alpha + FrameTime! * 4 )
            elseif alpha > 0
                alpha = math.max( 0, alpha - FrameTime! * 4 )

            if alpha == 0
                return

            SetAlphaMultiplier( alpha )

            iconSize = VMin( 12 )
            x, y = Jailbreak.ScreenCenterX, Jailbreak.ScreenHeight - iconSize / 2
            rotation = sin( CurTime! * 3 ) * 30

            SetMaterial( SpeakingIcon )

            SetDrawColor( black.r, black.g, black.b, 50 )
            DrawTexturedRectRotated( x - 1, y - 1, iconSize, iconSize, rotation )

            SetDrawColor( black.r, black.g, black.b, 120 )
            DrawTexturedRectRotated( x + 3, y + 3, iconSize, iconSize, rotation )

            SetDrawColor( 255, 255, 255 )
            DrawTexturedRectRotated( x, y, iconSize, iconSize, rotation )

            text = GetPhrase( "jb.speaking" )
            SetFont( "Jailbreak::VoiceChatIcon" )
            textWidth, textHeight = GetTextSize( text )
            x, y = ( Jailbreak.ScreenWidth - textWidth ) / 2, Jailbreak.ScreenHeight - iconSize - textHeight

            SetTextPos( x - 1, y - 1 )
            SetTextColor( black.r, black.g, black.b, 50 )
            DrawText( text )

            SetTextPos( x + 3, y + 3 )
            SetTextColor( black.r, black.g, black.b, 120 )
            DrawText( text )

            SetTextColor( 255, 255, 255 )
            SetTextPos( x, y )
            DrawText( text )

            SetAlphaMultiplier( 1 )

    -- Player status icons
    do

        :StatusIcons, :VoiceChatMaxDistance = Jailbreak
        :SetMaterial, :DrawSprite = render
        LocalToWorld = LocalToWorld
        :GetAll = player

        TypingIcon = Material( "icon16/comment_edit.png", "mips" )

        hook.Add "PostDrawTranslucentRenderables", "Jailbreak::StatusIcons", ( drawingDepth, drawingSkybox ) ->
            if drawingDepth or drawingSkybox or not StatusIcons\GetBool!
                return

            eyePos = EyePos!
            for ply in *GetAll!
                if ply\IsLocalPlayer! or not ply\IsInGame!
                    continue

                isSpeaking = ply\IsSpeaking!
                unless isSpeaking or ply\IsTyping!
                    continue

                origin = nil

                headBoneID = ply\LookupBone( "ValveBiped.Bip01_Head1" )
                if headBoneID and headBoneID >= 0
                    origin, angles = ply\GetBonePosition( headBoneID )

                    hitboxset = ply\GetHitboxSet!
                    for hitbox = 0, ply\GetHitBoxCount( hitboxset )
                        if headBoneID == ply\GetHitBoxBone( hitbox, hitboxset )
                            mins, maxs = ply\GetHitBoxBounds( hitbox, hitboxset )
                            origin = LocalToWorld( ( maxs + mins ) / 2, angle_zero, origin, angles ) + angles\Forward! * ( maxs[ 3 ] - mins[ 3 ] ) * 1.5
                            break

                else
                    origin = ply\EyePos!
                    origin[ 3 ] += 14

                distance = origin\Distance( eyePos )
                if distance > VoiceChatMaxDistance\GetInt!
                    continue

                origin[ 3 ] = 1 + origin[ 3 ] + sin( CurTime! * 4 ) * 1.5
                SetMaterial( isSpeaking and SpeakingIcon or TypingIcon )
                DrawSprite( origin, 12, 12, white )
