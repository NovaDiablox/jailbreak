AddCSLuaFile( "cl_init.lua" )
AddCSLuaFile( "shared.lua" )
include( "shared.lua" )

SWEP.UseDoor = ( open ) =>
    curTime, lastDoorLock = CurTime!, @LastDoorLock or 0
    @LastDoorLock = curTime

    if curTime - lastDoorLock <= 0.025
        return

    owner = @GetOwner!
    start = owner\GetShootPos!
    traceResult = util.TraceLine( {
        start: start
        endpos: start + owner\GetAimVector! * 64
        filter: owner
    } )

    unless traceResult.Hit
        return

    entity = traceResult.Entity
    unless IsValid( entity )
        return

    className = entity\GetClass!
    if className == "prop_door_rotating"
        unless owner\HasSecurityKeys!
            if entity\IsDoorLocked!
                entity\EmitSound( "physics/wood/wood_crate_impact_hard2.wav", 100, math.random( 90, 110 ), 1, CHAN_STATIC, 0, 1 )
                owner\AnimRestartNetworkedGesture( GESTURE_SLOT_ATTACK_AND_RELOAD, ACT_HL2MP_GESTURE_RANGE_ATTACK_FIST, true )
            else
                entity\Use( owner, owner )

            return

        if open ~= entity\IsDoorLocked! or entity\GetDoorState! ~= 0
            return

        timer.Simple 0.8, ->
            if entity\IsValid!
                entity\EmitSound( "doors/door_latch3.wav", 50, math.random( 80, 120 ), 1, CHAN_STATIC, 0, 1 )

        owner\EmitSound( "npc/metropolice/gear" .. math.random( 1, 6 ) .. ".wav", 50, math.random( 80, 120 ), 1, CHAN_STATIC, 0, 1 )
        owner\AnimRestartNetworkedGesture( GESTURE_SLOT_ATTACK_AND_RELOAD, ACT_GMOD_GESTURE_ITEM_PLACE, true )
        entity\Fire( open and "unlock" or "lock", "", 0, owner, owner )

    elseif className == "func_button"
        entity\EmitSound( "buttons/blip1.wav", 70, math.random( 90, 110 ), 1, CHAN_STATIC, 0, 1 )
        entity\Use( owner, owner )

do

    ConVarFlags = bit.bor( FCVAR_ARCHIVE, FCVAR_NOTIFY, FCVAR_REPLICATED, FCVAR_DONTRECORD )
    SWEP.MinDamage = CreateConVar( "jb_hands_damage_min", "8", ConVarFlags, "Minimal damage from a fist punch.", 0, 16384 )
    SWEP.MaxDamage = CreateConVar( "jb_hands_damage_max", "12", ConVarFlags, "Maximal damage from a fist punch.", 0, 16384 )

do

    phys_pushscale = GetConVar( "phys_pushscale" )
    NULL = NULL

    SWEP.HitEntity = ( entity, owner, origin ) =>
        hit, anim = false, @GetSequenceName( owner\GetViewModel!\GetSequence! )

        if IsValid( entity )
            scale = phys_pushscale\GetFloat!

            attacker = owner
            unless IsValid( attacker )
                attacker = @

            damageInfo = DamageInfo!
            damageInfo\SetInflictor( @ )
            damageInfo\SetAttacker( attacker )
            damageInfo\SetDamageType( DMG_SLASH )
            damageInfo\SetDamage( math.random( @MinDamage\GetInt!, @MaxDamage\GetInt! ) )
            damageInfo\SetDamagePosition( origin )

            if anim == "fists_left"
                damageInfo\SetDamageForce( ( owner\GetForward! + owner\GetRight! ) * 1000 * scale )
            elseif anim == "fists_right"
                damageInfo\SetDamageForce( ( owner\GetForward! - owner\GetRight! ) * 1000 * scale )
            elseif anim == "fists_uppercut"
                damageInfo\SetDamageForce( ( owner\GetForward! + owner\GetUp! ) * 1000 * scale )
                damageInfo\ScaleDamage( 2 )

            -- Let the breakable gibs spawn in multiplayer on client
            SuppressHostEvents( NULL )
            entity\TakeDamageInfo( damageInfo )
            SuppressHostEvents( owner )
            hit = true

        if hit and anim ~= "fists_uppercut"
            @SetCombo( @GetCombo! + 1 )
        else
            @SetCombo( 0 )

SWEP.Materials = {
    [ MAT_WOOD ]: 5
    [ MAT_VENT ]: 15
    [ MAT_GLASS ]: 15
    [ MAT_METAL ]: 10
    [ MAT_GRATE ]: 10
    [ MAT_CONCRETE ]: 10
}

SWEP.HitMaterial = ( matType, owner, origin ) =>
    damage = @Materials[ matType ]
    if damage ~= nil
        damageInfo = DamageInfo!
        damageInfo\SetInflictor( @ )
        damageInfo\SetAttacker( owner )
        damageInfo\SetDamagePosition( origin )
        damageInfo\SetDamage( math.ceil( owner\Health! * ( damage / 100 ) ) )
        damageInfo\SetDamageForce( owner\GetAimVector! * -( Jailbreak.PowerfulPlayers and 1000 or 100 ) )
        util.ScreenShake( owner\EyePos!, 12, 120, 0.5, 32 )

        if matType == MAT_COMPUTER
            damageInfo\SetDamageType( DMG_SHOCK )

            if owner\ShockCollarIsEnabled!
                damageInfo\ScaleDamage( 2 )

        else
            damageInfo\SetDamageType( DMG_SLASH )

        owner\TakeDamageInfo( damageInfo )

SWEP.OnDrop = =>
    @Remove!
