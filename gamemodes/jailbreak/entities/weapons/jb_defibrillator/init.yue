AddCSLuaFile( "cl_init.lua" )
AddCSLuaFile( "shared.lua" )
include( "shared.lua" )

CHAN_STATIC = CHAN_STATIC
DMG_SHOCK = DMG_SHOCK
:TraceLine = util
CurTime = CurTime
:random = math
trace = {}

jb_defibrillator_usages = CreateConVar( "jb_defibrillator_usages", "3", bit.bor( FCVAR_ARCHIVE, FCVAR_NOTIFY ), "Determines maximum number of defibrillator usages before it fails." )

SWEP.Initialize = =>
    @SetHoldType( "duel" )
    @SetClip1( jb_defibrillator_usages\GetInt! )

SWEP.TakeClip1 = ( amount, owner ) =>
    clip1 = @Clip1! - amount
    if clip1 < 1
        owner\EmitSound( "physics/metal/metal_box_break" .. random( 1, 2 ) .. ".wav", 75, random( 80, 120 ), 0.8, CHAN_STATIC, 0, 1 )
        owner\SelectWeapon( "jb_hands" )
        @Remove!
        return

    @SetClip1( clip1 )

SWEP.ShockAlivePlayer = ( ply, owner ) =>
    damageInfo = DamageInfo!
    damageInfo\SetAttacker( owner )
    damageInfo\SetInflictor( @ )
    damageInfo\SetDamage( random( 50, 100 ) )
    damageInfo\SetDamageType( DMG_SHOCK )

    ply\TakeDamageInfo( damageInfo )

    unless ply\Alive!
        owner\SendNotify( "#jb.notify.player-reanimated", NOTIFY_ERROR, 10, ply\Nick! )

SWEP.PrimaryAttack = =>
    owner = @GetOwner!
    unless owner\IsValid! and owner\Alive!
        return

    trace.start = owner\GetShootPos!
    trace.endpos = trace.start + owner\GetAimVector! * 128
    trace.filter = owner

    traceResult = TraceLine( trace )
    unless traceResult.Hit
        return

    entity = traceResult.Entity
    unless entity\IsValid!
        return

    entity\DoElectricSparks( traceResult.HitPos )
    @SetNextPrimaryFire( CurTime! + @UsageDelay\GetFloat! )

    unless entity\IsPlayerRagdoll!
        if entity\IsPlayer! and entity\Alive! and entity\Team! ~= owner\Team!
            @ShockAlivePlayer( entity, owner )
            @TakeClip1( 1, owner )
            return

        owner\SendNotify( "#jb.error.cant-do-that", NOTIFY_ERROR, 5 )
        return

    ply = entity\GetRagdollOwner!
    unless ply\IsValid!
        owner\SendNotify( "#jb.error.player-soulless", NOTIFY_ERROR, 10, entity\GetRagdollOwnerNickname! )
        return

    if ply\Alive!
        if entity\Alive! and ply\GetNW2Bool( "lost-consciousness" )
            @ShockAlivePlayer( ply, owner )
            @TakeClip1( 1, owner )
            return

        owner\SendNotify( "#jb.error.cant-do-that", NOTIFY_ERROR, 10 )
        return

    spawnTime = ply\GetSpawnTime!
    entity\SetAlive( true )
    ply\Spawn!
    ply\SetHealth( 25 )
    ply\SetNW2Int( "spawn-time", spawnTime )
    owner\SendNotify( "#jb.notify.player-reanimated", NOTIFY_GENERIC, 10, ply\Nick! )
    @TakeClip1( 1, owner )
