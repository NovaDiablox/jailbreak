AddCSLuaFile( "cl_init.lua" )

ENT.Type = "anim"
ENT.Sequence = "idle"

ENT.Initialize = =>
    @SetModel( @Model )

    @SetPos( @Origin )
    @SetAngles( @Angles )
    @SetTrigger( true )

    @SetCollisionGroup( COLLISION_GROUP_WEAPON )
    @SetMoveType( MOVETYPE_NONE )
    @PhysicsInit( SOLID_OBB )

    @ResetSequence( @Sequence )
    @Init!

ENT.Action = ->
ENT.Init = ->

ENT.Touch = ( entity ) =>
    if @IsDisabled!
        return

    if entity\IsPlayer! and entity\Alive!
        @Action( entity )

ENT.IsDisabled = =>
    return @Disabled or false

ENT.Disable = =>
	@Disabled = true
    @SetNoDraw( @Disabled )

ENT.Enable = =>
	@Disabled = false
    @SetNoDraw( @Disabled )

ENT.Toggle = =>
	@Disabled = not @Disabled
    @SetNoDraw( @Disabled )

ENT.AcceptInput = ( key, activator, caller, data ) =>
    switch key
        when "Disable"
            @Disabled = true
        when "Enable"
            @Disabled = false
        when "Toggle"
            @Disabled = not @Disabled

ENT.KeyValue = ( key, value ) =>
    switch key
        when "origin"
            @Origin = Vector( value )
        when "angles"
            @Angles = Angle( value )
        when "StartDisabled"
            @Disabled = tobool( value )
