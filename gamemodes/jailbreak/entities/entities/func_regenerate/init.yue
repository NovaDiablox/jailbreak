ENT.Type = "brush"
ENT.Sound = Sound( "Regenerate.Touch" )

ENT.Initialize = =>
    @SetTrigger( true )
    @Players = {}

ENT.Regenerate = ( ply ) =>
    ply\SetHealth( math.max( ply\Health!, ply\GetMaxHealth! ) )

ENT.StartTouch = ( entity ) =>
    if entity\IsPlayer! and entity\Alive!
        @Players[] = entity

ENT.EndTouch = ( entity ) =>
    if entity\IsPlayer!
        table.RemoveByValue( @Players, entity )

ENT.AssociatedAction = ( func ) =>
    for entity in *ents.FindByName( @AssociatedName )
        func( entity )

ENT.Open = =>
    if @Opened
        return

    @Opened = true
    @AssociatedAction ( entity ) ->
        entity\ResetSequence( "open" )
        entity\EmitSound( @Sound, 150 )

ENT.Close = =>
    unless @Opened
        return

    @Opened = false
    @AssociatedAction ( entity ) ->
        entity\ResetSequence( "close" )

ENT.ToggleOpen = =>
    if @Opened
        @Close!
    else
        @Open!

ENT.Think = =>
    if @IsDisabled!
        return

    players = @Players
    if #players == 0
        @Close!
        return

    for ply in *players
        @Regenerate( ply )

    @NextThink( CurTime! + 1 )
    @ToggleOpen!
    return true

ENT.IsDisabled = =>
    return @Disabled or false

ENT.Disable = =>
	@Disabled = true

ENT.Enable = =>
	@Disabled = false

ENT.Toggle = =>
	@Disabled = not @Disabled

ENT.AcceptInput = ( key, activator, caller, data ) =>
    func = @[ key ]
    if func
        func( @ )

ENT.KeyValue = ( key, value ) =>
	if key == "associatedmodel"
        @AssociatedName = value
    elseif key == "StartDisabled"
		@Disabled = tobool( value )
